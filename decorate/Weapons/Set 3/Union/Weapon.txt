/*
CREDITS:
Gun sprite - DoomNukem
Gun edits - Chronoteeth, CarbineDioxide
Pickup base - Railgunner
Gunflash - Bane
Also edited by me, Pillow~

Up sound - FEAR Expansion pack (TimeGate Studios/Monolith)
Fire sound - FEAR (Monolith)
Shot sound - Broforce (Free Lives)
Heal sound - Tiberian Sun (Westwood)
*/

Actor UnionShotCycle : Inventory { Inventory.MaxAmount 1 }
Actor UnionBurstMode : Inventory { Inventory.MaxAmount 1 }
Actor UnionBurst : Inventory { Inventory.MaxAmount 5 }

actor UnionGiver : CustomInventory
{
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Received ''Union'' high power rifle! Welp,its time to farm health"
	Obituary "%o was sent to the rest of bourgeoisie by %k's ''Union'' rifle."
	States
	{
	Spawn:
		UNRG X 1 
		Loop
  Pickup:
	TNT1 A 0 A_JumpIfInventory("Union",1,"JustAmmo")
  All:
	TNT1 A 0 A_GiveInventory("Union",1)
	Stop
  JustAmmo:
	TNT1 A 0 A_GiveInventory("Boolet",30)
  Remove:
    TNT1 A 0
    Stop
	}
}

Actor Union : DoomWeapon 24532
{
	//$Category Weapons
	//$Title Union
	//$Sprite UNRGX0
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Received ''Union'' high power rifle! Welp,its time to farm health"
	Obituary "%o was sent to the rest of bourgeoisie by %k's ''Union'' rifle."
	Weapon.UpSound "Union/Up"
	Weapon.SelectionOrder 5
	Weapon.AmmoUse 1
	Weapon.AmmoGive 30
	Weapon.AmmoType "Boolet"
	Weapon.AmmoUse2 6
	Weapon.AmmoGive2 0
	Weapon.AmmoType2 "BooletCasing"
	Weapon.SlotNumber 4
	Weapon.SlotPriority 28.0
	Weapon.Kickback 45
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobSpeed 1.4
	Weapon.BobRangeY 0.6
	Weapon.BobRangeX 1.2
	Decal "BigBulletScorch"
	+ALT_AMMO_OPTIONAL
	Scale 0.8
	States
	{
	Ready:
		UNRG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,11,1,0)
		UNRG A 0 {
			A_ZoomFactor(1.0);
		}
	DeselectMain:
		UNRG A 1 A_Lower(12)
		Loop
	Select:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,10,1,0)
		UNRG A 0 {
			If(random(1,100) <= GetCvar("RO_Voices")) {
				A_PlaySound("Union/Taunt",2);
			}
		}
	SelectMain:
		UNRG A 1 A_Raise(12)
		Loop
	Reload:
		UNRG A 0 A_PlaySound("Overwhelming/Mode",6,0.8)
		UNRG A 1 A_WeaponOffset(1.0, 1.0, WOF_ADD|WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(2.0, 3.0, WOF_ADD|WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(3.0, 4.0, WOF_ADD|WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(3.0, 3.0, WOF_ADD|WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(3.0, 2.0, WOF_ADD|WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(1.0, 1.0, WOF_ADD|WOF_INTERPOLATE)
		UNRG A 0 A_JumpIfInventory("UnionBurstMode",1,"UnReload")
		UNRG A 0 A_GiveInventory("UnionBurstMode",1)
		UNRG A 0 A_Print("\c[white]Burst mode \c[green]on.")
		Goto ReloadFinish
	UnReload:
		UNRG A 0 A_TakeInventory("UnionBurstMode",1)
		UNRG A 0 A_Print("\c[white]Burst mode \c[red]off.")
		Goto ReloadFinish
	ReloadFinish:
		UNRG A 1 A_WeaponOffset(-1.0, -1.0, WOF_ADD|WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(-3.0, -2.0, WOF_ADD|WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(-3.0, -3.0, WOF_ADD|WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(-3.0, -4.0, WOF_ADD|WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(-2.0, -3.0, WOF_ADD|WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(-1.0, -1.0, WOF_ADD|WOF_INTERPOLATE)
		UNRG A 10 A_Weaponready(WRF_NOSWITCH|WRF_NOBOB|WRF_NOSWITCH)
		Goto ready
	Fire:
		UNRF A 0 Bright {
			if(CountInv("UnionBurstMode"))
			{
				A_GiveInventory("UnionBurst",5);
			}
		}
	BurstFire:
		UNRF A 1 Bright {
			A_SetPitch(Pitch - (frandom(0.1,0.5) * CountInv("UnionBurst")) * GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - (frandom(0.1,0.5) * CountInv("UnionBurst")) * GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			if(GetCvar("RO_QuakeRecoil")) { A_QuakeEx(1,1,1,8,0,32,"none",QF_SCALEDOWN); }
			A_PlaySound("Union/Fire",1);
			If(CountInv("UnionShotCycle"))
			{
				A_TakeInventory("UnionShotCycle");
				A_PlaySound("Union/Shot",6,0.9);
			}
			Else
			{
				A_GiveInventory("UnionShotCycle");
				A_PlaySound("Union/Shot",5,0.9);
			}
			A_GiveInventory("BooletCasing",1);
			A_GunFlash;
			A_WeaponOffset(1.0,33.0,WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.995); }
			If(!CountInv("CaliberUpgrade"))
			{
				A_FireBullets(3.5,2.0,1,75,"UnionPuff",FBF_USEAMMO,8192,"UnionTracer",0,9);
			}
			Else
			{
				A_FireBullets(3.5,2.0,1,150,"UnionCaliberPuff",FBF_USEAMMO,8192,"UnionTracer",0,9);
				A_PlaySound("Weapons/WolgirHEndFire",4);
			}
		}
		UNRF B 1 Bright { A_WeaponOffset(4.0 + frandom(4.0, -4.0), 36.0 + frandom(0.0, 4.0), WOF_INTERPOLATE); if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.99); } }
		UNRF B 0 {
			A_TakeInventory("UnionBurst",1);
			if(CountInv("UnionBurst"))
			{
				return state("BurstFire");
			}
			return state("");
		}
	Mainis:
		UNRF C 1 		{ A_WeaponOffset(7.0 + frandom(2.0, -2.0), 39.0 + frandom(0.0, 2.0), WOF_INTERPOLATE); if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.995); } }
		UNRF D 1		{ A_WeaponOffset(9.0 + frandom(1.0, -1.0), 41.0 + frandom(0.0, 1.0), WOF_INTERPOLATE); if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.9975); } Return A_Jump(128,1); }
		UNRF E 0 A_ZoomFactor(1.0)
		UNRF E 0 A_JumpIf(CountInv("UnionBurstMode"),2)
		UNRF D 0 A_ReFire("Fire")
		UNRF E 1
		UNRF E 1 A_WeaponOffset(8.0,40.0, WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(6.0,38.0, WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(3.0,35.0, WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(1.0,33.0, WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(0.0,32.0, WOF_INTERPOLATE)
		Goto Ready
	AltFire:
		UNRH A 0 A_JumpIf(!CountInv("BooletCasing"),"AltDry")
		UNRH A 0 Bright {
			A_SpawnItemEx("UnionHealEffect",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_ORIGINATOR|SXF_SETMASTER,0);
			if(CountInv("UnionBurstMode"))
			{
				A_GiveInventory("UnionBurst",5);
			}
		}
	AltBurstFire:
		UNRH A 0 A_JumpIf(!CountInv("BooletCasing"),"AltDry")
		UNRH A 1 Bright {
			A_SetPitch(Pitch - (frandom(0.1,0.5) * CountInv("UnionBurst")) * GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - (frandom(0.1,0.5) * CountInv("UnionBurst")) * GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			if(GetCvar("RO_QuakeRecoil")) { A_QuakeEx(1,1,1,8,0,32,"none",QF_SCALEDOWN); }
			If(CountInv("UnionShotCycle"))
			{
				A_TakeInventory("UnionShotCycle");
				A_PlaySound("Union/Heal",6);
			}
			Else
			{
				A_GiveInventory("UnionShotCycle");
				A_PlaySound("Union/Heal",5);
			}
			A_GunFlash;
			A_WeaponOffset(1.0,33.0, WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.995); }
			A_GiveInventory("UnionHealItem");
			A_FireBullets(3.5,2.0,1,75,"UnionHealPuff",FBF_USEAMMO,8192,"UnionHealTracer",0,9);
		}
		UNRH B 1 Bright { A_WeaponOffset(4.0 + frandom(4.0, -4.0), 36.0 + frandom(0.0, 4.0), WOF_INTERPOLATE); A_ZoomFactor(0.99); }
		UNRF B 0 {
			A_TakeInventory("UnionBurst",1);
			if(CountInv("UnionBurst"))
			{
				return state("AltBurstFire");
			}
			return state("");
		}
	AltMainis:
		UNRF C 1 { A_WeaponOffset(7.0 + frandom(2.0, -2.0), 39.0 + frandom(0.0, 2.0), WOF_INTERPOLATE); if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.995); } }
		UNRF D 1 { A_WeaponOffset(9.0 + frandom(1.0, -1.0), 41.0 + frandom(0.0, 1.0), WOF_INTERPOLATE); if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.9975); } }
		UNRF E 0 A_ZoomFactor(1.0)
		UNRF E 0 A_JumpIf(CountInv("UnionBurstMode"),3)
		UNRF E 1 A_Jump(128,1)
		UNRF D 0 A_ReFire("AltFire")
		UNRF E 1 A_WeaponOffset(8.0,40.0, WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(6.0,38.0, WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(3.0,35.0, WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(1.0,33.0, WOF_INTERPOLATE)
		UNRG A 1 A_WeaponOffset(0.0,32.0, WOF_INTERPOLATE)
		Goto Ready
	AltDry:
		UNRG A 0 A_PlaySound("Union/Dry",1)
		UNRF C 1 A_WeaponOffset(1.0,33.0, WOF_INTERPOLATE)
		UNRF D 1 A_WeaponOffset(4.0,36.0, WOF_INTERPOLATE)
		UNRF D 1 A_WeaponOffset(3.0,35.0, WOF_INTERPOLATE)
		UNRF E 1 A_WeaponOffset(2.0,34.0, WOF_INTERPOLATE)
		UNRF E 1 A_WeaponOffset(1.0,33.0, WOF_INTERPOLATE)
		UNRG A 10 { A_WeaponOffset(0.0,32.0, WOF_INTERPOLATE); A_WeaponReady(WRF_NOSECONDARY); }
		Goto Ready
	Flash:
		TNT1 A 2 Bright A_Light1
		TNT1 A 2 Bright A_Light2
		Goto LightDone
	Spawn:
		UNRG X -1
		Stop
	}
}