/*
CREDITS:
Gun graphic source - Lt.Kjilgore on GameBanana. (Formerly FPS Banana.)
Gun graphic rip - Zero Prophet's Zero Tolerance. Excelent mod, btw!
Grenade elemental effects - Heretic (Raven software)
Grenade projectile - ??
Frozen orb graphic - ??

Sounds:
Up, Add, Open, Load, Slide, Close - ??
Fire, Hit - Will Rock (Ubisoft)
Fire and Ice Grenades - Diablo II (Blizzard Ent.)

Toxic Grenades:
Toxic Rain - Wolfenstein:ET (ID Soft.)
Pod explode - Will Rock (Ubisoft), methinks.

*/

ACTOR NormalModeTrigger : Inventory { Inventory.MaxAmount 1 }
ACTOR FlameModeTrigger : NormalModeTrigger {}
ACTOR FrostModeTrigger : NormalModeTrigger {}
ACTOR ToxicModeTrigger : NormalModeTrigger {}

ACTOR FlameMode : NormalModeTrigger {}
ACTOR FrostMode : NormalModeTrigger {}
ACTOR ToxicMode : NormalModeTrigger {}

ACTOR RasputinInfo : NormalModeTrigger {}
ACTOR RasputinHeld : NormalModeTrigger {}
ACTOR RasputinAlt : NormalModeTrigger {}

actor RasputinGiver : CustomInventory
{
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got the ''Rasputin'' elemental shell launcher! 4 modes,every with his one unique purpose..."
	Obituary "%o was blasted by %k's ''Rasputin'' grenade."
	States
	{
	Spawn:
		GENG X 1 
		Loop
  Pickup:
	TNT1 A 0 A_JumpIfInventory("Rasputin",1,"JustAmmo")
  All:
	TNT1 A 0 A_GiveInventory("Rasputin",1)
	Stop
  JustAmmo:
	TNT1 A 0 A_GiveInventory("TankShellAmmo",10)
  Remove:
    TNT1 A 0
    Stop
	}
}

ACTOR Rasputin : Weapon 24534
{
	//$Category Weapons
	//$Title Rasputin
	//$Sprite GENGX0
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got the ''Rasputin'' elemental shell launcher! 4 modes,every with his one unique purpose..."
	Obituary "%o was blasted by %k's ''Rasputin'' grenade."
	Weapon.UpSound "Rasputin/Up"
	Weapon.SelectionOrder 4
	Weapon.AmmoType "TankShellAmmo"
	Weapon.AmmoGive 10
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "TankShellAmmo"
	Weapon.AmmoGive2 0
	Weapon.AmmoUse2 0
	Weapon.Kickback 50
	Weapon.SlotNumber 5
	Weapon.SlotPriority 28.0
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 1.4
	Weapon.BobRangeY 0.6
	Weapon.BobRangeX 1.2
	Scale 0.8
	+NOALERT
	States
	{
	Spawn:
		GENG X -1
		Stop
	Ready:
		GE1G A 0 {
			/*if(!CountInv("RasputinInfo") && !GetCvar("RO_IGotIt"))
			{
				A_GiveInventory("RasputinInfo",1);
				A_Print("$RASPUTIN_INTEL",8);
			}*/
		}
		GE4G A 0 A_JumpIf(CountInv("ToxicMode"), "ReadyMain")
		GE3G A 0 A_JumpIf(CountInv("FrostMode"), "ReadyMain")
		GE2G A 0 A_JumpIf(CountInv("FlameMode"), "ReadyMain")
		GE1G A 0
	ReadyMain:
		"####" A 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWRELOAD)
		Loop
   Select:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,0,1,0)
		GE4G A 0 A_JumpIf(CountInv("ToxicMode"), "SelectMain")
		GE3G A 0 A_JumpIf(CountInv("FrostMode"), "SelectMain")
		GE2G A 0 A_JumpIf(CountInv("FlameMode"), "SelectMain")
		GE1G A 0
		"####" A 1 A_Raise(12)
		Wait
	Deselect:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,1,1,0)
	DeselectLoop:
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 1 A_Lower(12)
		Wait
	Fire:
		GE4F A 0 A_JumpIf(CountInv("ToxicMode") && CountInv("TankShellAmmo") >= 3, "ToxicFire")
		GE3F A 0 A_JumpIf(CountInv("FrostMode") && CountInv("TankShellAmmo") >= 5, "FrostFire")
		GE2F A 0 A_JumpIf(CountInv("FlameMode") && CountInv("TankShellAmmo") >= 5, "FlameFire")
		GE1F A 0 A_JumpIf(CountInv("TankShellAmmo") > 0
					   && !CountInv("ToxicMode")
					   && !CountInv("FrostMode")
					   && !CountInv("FlameMode"), "RegularFire")
		GE1G A 0 A_CheckReload
		Goto Ready
	ToxicFire:
		"####" A 0 {
			A_FireCustomMissile("RasputinToxicShell",0,0,9);
			A_TakeInventory("TankShellAmmo",3,TIF_NOTAKEINFINITE);
		}
		Goto FireAnim
	FrostFire:
		"####" A 0 {
			A_FireCustomMissile("RasputinFrostShell",0,0,9);
			A_TakeInventory("TankShellAmmo",5,TIF_NOTAKEINFINITE);
		}
		Goto FireAnim
	FlameFire:
		"####" A 0 {
			A_FireCustomMissile("RasputinFlameShell",0,0,9);
			A_TakeInventory("TankShellAmmo",5,TIF_NOTAKEINFINITE);
		}
		Goto FireAnim
	RegularFire:
		"####" A 0 {
			A_FireCustomMissile("RasputinShell",0,0,9);
			A_TakeInventory("TankShellAmmo",1,TIF_NOTAKEINFINITE);
		}
	FireAnim:
		"####" A 0 Bright {
			A_GunFlash;
			A_Recoil(4.0);
			A_AlertMonsters;
			A_PlaySound("Rapsutin/Fire",5,0.9);
			A_PlaySound("Rapsutin/Add",1,0.9);
			A_PlaySound("Weapons/PushkaFire",6);
			A_SetPitch(Pitch - 4.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 3.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" A 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.97); } A_WeaponOffset(3.0,35.0,WOF_INTERPOLATE); }
		"####" B 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.95); } A_WeaponOffset(7.0,41.0,WOF_INTERPOLATE); }
		"####" C 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.97); } A_WeaponOffset(9.0,43.0,WOF_INTERPOLATE); }
		"####" D 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.98); } A_WeaponOffset(10.0,46.0,WOF_INTERPOLATE); }
		"####" E 1		  { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.99); } }
		"####" E 1		  { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.995); } A_WeaponOffset(8.0,45.0,WOF_INTERPOLATE); }
		"####" E 1		  { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(1.0); }	A_WeaponOffset(6.0,42.0,WOF_INTERPOLATE); }
		"####" F 1																A_WeaponOffset(4.0,39.0,WOF_INTERPOLATE)
		"####" F 1																A_WeaponOffset(3.0,36.0,WOF_INTERPOLATE)
		"####" G 1																A_WeaponOffset(1.0,33.0,WOF_INTERPOLATE)
		"####" G 1																A_WeaponOffset(0.0,32.0,WOF_INTERPOLATE)
		GE4F A 0 A_JumpIf(CountInv("ToxicMode") > 0 ||
						  CountInv("FrostMode") > 0 ||
						  CountInv("FlameMode") > 0, "LenghtierDelay")
		Goto RegularDelay
	RegularDelay:
		GE1G A 4 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		"####" A 0 A_ReFire
		Goto Ready
	LenghtierDelay:
		GE4G A 0 A_JumpIf(CountInv("ToxicMode"), "LenghtierMain")
		GE3G A 0 A_JumpIf(CountInv("FrostMode"), "LenghtierMain")
		GE2G A 0 A_JumpIf(CountInv("FlameMode"), "LenghtierMain")
		Goto RegularDelay
	LenghtierMain:
		"####" A 15 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE)
		"####" A 0 A_ReFire
		Goto Ready
   Flash:
      TNT1 A 4 A_Light2
      TNT1 A 6 A_Light1
      TNT1 A 0 A_Light0
      Stop
	  
	AltFire: 
		"####" "#" 0 A_GiveInventory("RasputinAlt", 1)
		"####" "#" 0 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & (BT_LEFT | BT_MOVELEFT), "FlameUnloadSeq")
		"####" "#" 0 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & BT_FORWARD, "FrostUnloadSeq")
		"####" "#" 0 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & (BT_RIGHT | BT_MOVERIGHT), "ToxicUnloadSeq")
		"####" "#" 1 // kd: Empty frame so the info ticker can show stuff!
		"####" "#" 0 A_TakeInventory("RasputinAlt", 1)
		Goto ReadyMain
		
	FlameUnloadSeq:
		"####" A 0 {
			If(CountInv("FlameMode")) {
				A_TakeInventory("FlameMode",1);
				Return State("UnloadAnim");
			} Else {
				A_GiveInventory("FlameMode",1);
				A_TakeInventory("FrostMode",1);
				A_TakeInventory("ToxicMode",1);
				Return State("UnloadAnim");
			}
		}
	FrostUnloadSeq:
		"####" A 0 {
			If(CountInv("FrostMode")) {
				A_TakeInventory("FrostMode",1);
				Return State("UnloadAnim");
			} Else {
				A_TakeInventory("FlameMode",1);
				A_GiveInventory("FrostMode",1);
				A_TakeInventory("ToxicMode",1);
				Return State("UnloadAnim");
			}
		}
	ToxicUnloadSeq:
		"####" A 0 {
			If(CountInv("ToxicMode")) {
				A_TakeInventory("ToxicMode",1);
				Return State("UnloadAnim");
			} Else {
				A_TakeInventory("FlameMode",1);
				A_TakeInventory("FrostMode",1);
				A_GiveInventory("ToxicMode",1);
				Return State("UnloadAnim");
			}
		}
		
	UnloadAnim:
		"####" B 1 A_WeaponOffset(1.0, 1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" C 1 A_WeaponOffset(2.0, 2.0, WOF_ADD|WOF_INTERPOLATE)
		"####" D 1 A_WeaponOffset(3.0, 3.0, WOF_ADD|WOF_INTERPOLATE)
		"####" E 1 A_WeaponOffset(4.0, 4.0, WOF_ADD|WOF_INTERPOLATE)
		"####" F 1 A_WeaponOffset(3.0, 3.0, WOF_ADD|WOF_INTERPOLATE)
		"####" G 1 A_WeaponOffset(1.0, 1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" H 1 A_WeaponOffset(1.0, 1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" H 4
		"####" I 2 A_PlaySound("Rasputin/Open",5)
		"####" J 1 A_WeaponOffset(3.0, 3.0, WOF_ADD|WOF_INTERPOLATE)
		"####" J 1 A_WeaponOffset(-1.0, -1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" J 1 A_WeaponOffset(-1.0, -1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" J 1 A_WeaponOffset(-1.0, -1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" J 2
		"####" K 0 A_PlaySound("Rasputin/Slide",6)
		"####" KLMNOP 1
		GE1G Q 10
		GE1G Q 0 A_TakeInventory("RasputinAlt", 1)
		
		GE4G A 0 A_JumpIf(CountInv("ToxicMode"), "LoadSeq")
		GE3G A 0 A_JumpIf(CountInv("FrostMode"), "LoadSeq")
		GE2G A 0 A_JumpIf(CountInv("FlameMode"), "LoadSeq")
		GE1G A 0
		Goto LoadSeq
		
	LoadSeq:
		"####" PONMLK 1
		"####" J 0 A_PlaySound("Rasputin/Tap",5)
		"####" J 1   A_WeaponOffset(-1.0, -1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" J 1   A_WeaponOffset(-3.0, -3.0, WOF_ADD|WOF_INTERPOLATE)
		"####" J 1   A_WeaponOffset(1.0, 1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" J 1   A_WeaponOffset(1.0, 1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" J 1   A_WeaponOffset(1.0, 1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" J 1   A_WeaponOffset(1.0, 1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" I 2
		"####" H 1 { A_WeaponOffset(2.0, 3.0, WOF_ADD|WOF_INTERPOLATE); A_PlaySound("Rasputin/Close",5); }
		"####" H 1   A_WeaponOffset(-0.5, -1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" H 1	 A_WeaponOffset(-1.0, -1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" H 1	 A_WeaponOffset(-0.5, -1.0, WOF_ADD|WOF_INTERPOLATE)
		
		"####" G 1 A_WeaponOffset(-1.0, -1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" F 1 A_WeaponOffset(-2.0, -2.0, WOF_ADD|WOF_INTERPOLATE)
		"####" E 1 A_WeaponOffset(-3.0, -3.0, WOF_ADD|WOF_INTERPOLATE)
		"####" D 1 A_WeaponOffset(-4.0, -4.0, WOF_ADD|WOF_INTERPOLATE)
		"####" C 1 A_WeaponOffset(-3.0, -3.0, WOF_ADD|WOF_INTERPOLATE)
		"####" B 1 A_WeaponOffset(-1.0, -1.0, WOF_ADD|WOF_INTERPOLATE)
		"####" A 1 A_WeaponOffset(-1.0, -1.0, WOF_ADD|WOF_INTERPOLATE)
		Goto Ready
	}
}
