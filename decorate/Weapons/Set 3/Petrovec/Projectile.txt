actor PetrovecBolt
{
	Radius 7
	Height 3
	Speed 30
	Damage 170
	Renderstyle "Add"
	Decal "PetrovecDecal"
	Alpha 0.99
	Scale 0.9
	Projectile
	+SEEKERMISSILE
	+SCREENSEEKER
	+DONTBLAST
	+MTHRUSPECIES
	+ROLLSPRITE
	+ROLLCENTER
	var int user_counter;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			A_PlaySound("Petrovec/Fly",5,1.0,1);
			A_SetRoll(frandom(0.0,360.0));
		}
	Main:
        NPSB A 1 Bright {
			A_SetRoll(Roll + frandom(6.0,8.0),SPF_INTERPOLATE);
			for(user_counter = 0; user_counter < 2; user_counter++)
			{
				A_SpawnItemEx("PetrovecBoltParticleTrail",
					frandom(3.0,-3.0),frandom(3.0,-3.0),frandom(3.0,-3.0),
					0,0,0,0,SXF_CLIENTSIDE,48);
				A_SpawnItemEx("PetrovecBoltSmokeTrail",
					frandom(4.0,-4.0),frandom(4.0,-4.0),frandom(4.0,-4.0),
					0,0,0,0,SXF_CLIENTSIDE,48);
			}
			/*A_SpawnItemEx("PetrovecBoltRingTrail",
				frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(2.0,-2.0),
				0,0,0,0,SXF_CLIENTSIDE,48);*/
			A_SeekerMissile(3,3,SMF_LOOK|SMF_PRECISE,256,1);
		}
		Loop
	Death:
		TNT1 A 0 {
			A_PlaySound("Petrovec/Hit",5);
			A_PlaySound("Petrovec/Explode",6);
			A_Explode(175,128,0,0);
			A_SpawnItemEx("PetrovecBoltRingExp");
			for(user_counter = 0; user_counter < 15; user_counter++)
			{
				A_SpawnItemEx("PetrovecBoltParticleExp",
					frandom(11.0,-11.0),frandom(11.0,-11.0),frandom(9.0,-9.0),
					frandom(1.0,2.0),frandom(1.0,2.0),frandom(2.0,-2.0),
					frandom(0.0,360.0),SXF_CLIENTSIDE,24);
			}
			for(user_counter = 0; user_counter < 7; user_counter++)
			{
				A_SpawnItemEx("PetrovecBoltExplode",
					frandom(19.0,-19.0),frandom(19.0,-19.0),frandom(12.0,-12.0),
					frandom(1.0,2.0),frandom(1.0,2.0),frandom(2.0,-2.0),
					frandom(0.0,360.0),SXF_CLIENTSIDE,24);
			}
		}
		TNT1 A 10
		TNT1 A 0 {
			for(user_counter = 0; user_counter < 7; user_counter++)
			{
				A_SpawnItemEx("PetrovecBoltSmokeExp",
					frandom(12.0,-12.0),random(12.0,-12.0),frandom(7.0,-7.0),
					frandom(1.0,-1.0),frandom(1.0,-1.0),frandom(1.0,-1.0),
					frandom(0.0,360.0),SXF_CLIENTSIDE,24);
			}
		}
		stop
	}
}

actor PetrovecBolt2 : PetrovecBolt
{
    BounceType "Hexen"
    BounceCount 10
    BounceFactor 1.0
    WallBounceFactor 1.0
	ReactionTime 210
    BounceSound "Petrovec/Bounce"
	Decal "PetrovecDecal"
	Projectile
    +BOUNCEONACTORS
	+DONTBLAST
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			A_PlaySound("Petrovec/Fly",5,1.0,1);
			A_SetRoll(frandom(0.0,360.0),SPF_INTERPOLATE);
		}
	Main:
        NPSB A 1 Bright {
			A_SetRoll(Roll + frandom(6.0,8.0),SPF_INTERPOLATE);
			for(user_counter = 0; user_counter < 2; user_counter++)
			{
				A_SpawnItemEx("PetrovecBoltParticleTrail",
					frandom(3.0,-3.0),frandom(3.0,-3.0),frandom(3.0,-3.0),
					0,0,0,0,SXF_CLIENTSIDE,48);
				A_SpawnItemEx("PetrovecBoltSmokeTrail",
					frandom(4.0,-4.0),frandom(4.0,-4.0),frandom(4.0,-4.0),
					0,0,0,0,SXF_CLIENTSIDE,48);
			}
			A_SpawnItemEx("PetrovecBoltRingTrail",
				frandom(2.0,-2.0),frandom(2.0,-2.0),frandom(2.0,-2.0),
				0,0,0,0,SXF_CLIENTSIDE,48);
			A_SeekerMissile(6,12,SMF_LOOK|SMF_PRECISE,256,1);
			A_Countdown;
		}
		Loop
	}
}

actor PetrovecBoltSmokeTrail
{
	Scale 0.35
	Alpha 0.95
	+NOINTERACTION
	Renderstyle Add
	States
	{
		Spawn:
			TNT1 A 2
			NPSS BDMKHFEDCBAA 1 Bright A_FadeOut(0.04)
			Stop
	}
}

actor PetrovecBoltRingTrail
{
	Scale 0.15
	Alpha 0.1
	+NOINTERACTION
	Renderstyle Add
	States
	{
		Spawn:
			TNT1 A 2
			NPSR AA 1 Bright {
				A_FadeIn(0.025);
				A_SetScale(ScaleX + 0.075);
			}
			NPSR AA 1 Bright {
				A_FadeIn(0.025);
				A_SetScale(ScaleX + 0.05);
			}
			NPSR A 1 Bright {
				A_FadeIn(0.025);
				A_SetScale(ScaleX + 0.025);
			}
		Reverse:
			NPSR A 1 Bright {
				A_FadeOut(0.025);
				A_SetScale(ScaleX - 0.025);
			}
			NPSR AA 1 Bright {
				A_FadeOut(0.025);
				A_SetScale(ScaleX - 0.05);
			}
			NPSR AA 1 Bright {
				A_FadeOut(0.025);
				A_SetScale(ScaleX - 0.075);
			}
			Stop
	}
}

actor PetrovecBoltParticleTrail
{
	Scale 0.04
	Alpha 0.99
	+NOINTERACTION
	Renderstyle Add
	States
	{
	Spawn:
		NPSB A 1 Bright {
			A_FadeOut(0.04 + frandom(0.0075,-0.0075));
			A_ChangeVelocity (frandom(0.2, -0.2),frandom(0.2, -0.2), frandom(0.2, -0.2), CVF_RELATIVE);
			A_SetScale(ScaleX - 0.00025);
			Return A_JumpIf(ScaleX <= 0, "Null");
		}
		Loop
	}
}

actor PetrovecBoltExplode
{
	Scale 2.0
	Alpha 0.75
	+NOINTERACTION
	Renderstyle Add
	States
	{
	Spawn:
		NPSS ABCDEFGHIJKLMNOP 2 Bright A_FadeOut(0.05)
		Wait
	}
}


actor PetrovecBoltSmokeExp
{
	Scale 1.75
	Alpha 0.5
	+NOINTERACTION
	Renderstyle Add
	States
	{
	Spawn:
		NPSS P 1 Bright NoDelay A_FadeOut(0.005)
		Wait
	}
}

actor PetrovecBoltRingExp
{
	Scale 0.25
	Alpha 0.99
	+NOINTERACTION
	Renderstyle Add
	States
	{
	Spawn:
		NPSR AAAA 1 Bright {
			A_FadeOut(0.05);
			A_SetScale(ScaleX + 0.1);
		}
	Looplet:
		NPSR A 1 Bright {
			A_FadeOut(0.03);
			A_SetScale(ScaleX + 0.1);
		}
		Loop
	}
}

actor PetrovecBoltParticleExp : PetrovecBoltParticleTrail
{
	Scale 0.1
	Alpha 0.99
	+NOINTERACTION
	Renderstyle Add
	States
	{
	Spawn:
		NPSB A 1 Bright {
			A_FadeOut(0.03 + frandom(0.0075,-0.0075));
			A_ChangeVelocity(frandom(0.6,-0.6),frandom(0.6,-0.6),frandom(0.6,-0.6),CVF_RELATIVE);
			A_SetScale(ScaleX - 0.00025);
			Return A_JumpIf(ScaleX <= 0.0, "Null");
		}
		Loop
	}
}