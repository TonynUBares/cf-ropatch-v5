/*
INFO - Frankensprite - a sprite made of parts of other sprites.
Just like frankenstein is a mashup of other people's limbs. Crazy, eh?

CREDITS:
Original idea by Apogee.

Sprites:
Frankensprite - Neccronixus
Bits from ID (Quake 4), 3DRealms (Duke Nukem 3D, Shadow Warrior)
Recolor, moving part on the right - Icytux
Pickup by me, with bits from 3DRealms and Eriance (Buttstock)
Flames by KeksDose

Sounds:
Fire - Raven (Wolfenstein 3)
Reload - People can Fly, Epic Games (Bulletstorm)
X-blast - ?
Firewall - Raven (Wolfenstein 3)
*/

ACTOR FireblastCounter : Inventory { Inventory.MaxAmount 4 }

actor TorchglowGiver : CustomInventory
{
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got the ''Torchglow'' firestorm cannon! The result of a russian kid playing with fire"
	Obituary "Like it was said - ''Torchglow'' scorched %o to ashes, all credit for shooting goes to %k."
	States
	{
	Spawn:
		FRGG X 1 
		Loop
  Pickup:
	TNT1 A 0 A_JumpIfInventory("Torchglow",1,"JustAmmo")
  All:
	TNT1 A 0 A_GiveInventory("Torchglow",1)
	Stop
  JustAmmo:
	TNT1 A 0 A_GiveInventory("TankShellAmmo",12)
  Remove:
    TNT1 A 0
    Stop
	}
}

ACTOR Torchglow : ROWeapon 24507
{
	//$Category Weapons
	//$Title Torchglow
	//$Sprite FRGGX0
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got the ''Torchglow'' firestorm cannon! The result of a russian kid playing with fire"
	Obituary "Like it was said - ''Torchglow'' scorched %o to ashes, all credit for shooting goes to %k."
	Weapon.UpSound "Torchglow/Up"
	Weapon.SelectionOrder 4
	Weapon.AmmoType "TankShellAmmo"
	Weapon.AmmoGive 12
	Weapon.AmmoUse 3
	Weapon.AmmoType2 "TankShellAmmo"
	Weapon.AmmoUse2 3
	Weapon.Kickback 60
	Weapon.SlotNumber 5
	Weapon.SlotPriority 29.0
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobSpeed 1.4
	Weapon.BobRangeY 0.6
	Weapon.BobRangeX 1.2
	+NOAUTOFIRE
	Scale 0.7
	States
	{
	Spawn:
      FRGG X -1
      Stop
	Ready:
		FRGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	Deselect:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,1,1,0)
	DeselectLoop:
		FRGG A 0 A_ZoomFactor(1.0)
		FRGG A 1 A_Lower(12)
		Wait
	Select:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,0,1,0)
	SelectLoop:
		FRGG A 1 A_Raise(12)
		Loop
	Fire:
		FRGF A 0 {
			A_FireCustomMissile("FirewallBomb",0,1,10,0);
			if(GetCvar("RO_ZoomRecoil")) { A_Overlay(-55,"StandardZoomRecoil"); }
		}
	FireAnim:
		FRGF A 0 { 
			A_GunFlash;
			A_Recoil(12.0);
			A_PlaySound("Torchglow/Fire",1);
			A_PlaySound("Torchglow/Add",5);
		}
		FRGF A 1 Bright { A_WeaponOffset(4.0, 4.0, WOF_ADD|WOF_INTERPOLATE); A_SetPitch(pitch - 2.0*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE); A_SetAngle(angle - 1.0*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE); }
		FRGF A 1 Bright { A_WeaponOffset(8.0, 8.0, WOF_ADD|WOF_INTERPOLATE); A_SetPitch(pitch - 4.0*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE); A_SetAngle(angle - 2.0*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE); }
		FRGF B 1 Bright { A_WeaponOffset(6.0, 6.0, WOF_ADD|WOF_INTERPOLATE); A_SetPitch(pitch - 2.0*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE); A_SetAngle(angle - 1.0*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE); }
		FRGF B 1 Bright { A_WeaponOffset(3.0, 3.0, WOF_ADD|WOF_INTERPOLATE); }
		FRGF C 1 Bright { A_WeaponOffset(1.0, 1.0, WOF_ADD|WOF_INTERPOLATE); }
		FRGF C 1 Bright { 									 				 }
	FireAnim2:
		FRGG A 1		{ A_WeaponOffset(-2.0, -2.0, WOF_ADD|WOF_INTERPOLATE); }
		FRGG A 1 		{ A_WeaponOffset(-3.0, -3.0, WOF_ADD|WOF_INTERPOLATE); }
		FRGG A 1 		{ A_WeaponOffset(-5.0, -5.0, WOF_ADD|WOF_INTERPOLATE); }
		FRGG A 1 		{ A_WeaponOffset(-6.0, -6.0, WOF_ADD|WOF_INTERPOLATE); }
		FRGG A 1 		{ A_WeaponOffset(-3.0, -3.0, WOF_ADD|WOF_INTERPOLATE); }
		FRGG A 1 		{ A_WeaponOffset(-2.0, -2.0, WOF_ADD|WOF_INTERPOLATE); }
		FRGG A 1 		  A_WeaponOffset(-1.0, -1.0, WOF_ADD|WOF_INTERPOLATE)
		FRGR A 2 A_PlaySound("Torchglow/Load",6,0.5)
		FRGR BBCCDD 1  	  A_WeaponOffset(-2.0, 2.0, WOF_ADD|WOF_INTERPOLATE)
		FRGG AAAAAA 1  	  A_WeaponOffset(2.0, -2.0, WOF_ADD|WOF_INTERPOLATE)
		FRGG A 2
		FRGG A 0 A_ReFire
		Goto Ready
   AltFire:
      FRGF A 0 {
			A_FireCustomMissile("FireblastMissile",0,1,10,0);
			if(GetCvar("RO_ZoomRecoil")) { A_Overlay(-55,"StandardZoomRecoil"); }
	   }
      Goto FireAnim
   Reload:
	   FRGF A 0 A_JumpIf(CountInv("TankShellAmmo"),1)
	   Goto Ready
	   FRGF A 0 A_GiveInventory("FireblastCounter",4) // I'd use an int but VM EXECUTION USED IN AMBIGOUS CONTEXT, FUCK YOU BALTIMOREEE~
	   FRGF A 0 {
			if(CountInv("FireblastCounter") <= 1) { A_TakeInventory("TankShellAmmo",1); }
			A_FireCustomMissile("FireblastRapidMissile",frandom(2.0,-2.0),0,10,0,0,frandom(2.0,-2.0));
			A_TakeInventory("FireblastCounter",1);
			A_GunFlash;
			if(GetCvar("RO_ZoomRecoil")) { A_Overlay(-55,"RapidZoomRecoil"); }
			A_Recoil(1.25);
			A_PlaySound("Torchglow/Fire",1);
			A_PlaySound("Torchglow/Add",5);
		}
		FRGF A 1 Bright { A_WeaponOffset(4.0+frandom(8.0,-8.0), 32+4.0+frandom(8.0,-8.0), WOF_INTERPOLATE);   A_SetPitch(pitch - (0.5 + frandom(0.2,-0.2)) * GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE); A_SetAngle(angle - (0.5 + frandom(0.2,-0.2)) * GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE); }
		FRGF B 1 Bright { A_WeaponOffset(11.0+frandom(4.0,-4.0), 32+11.0+frandom(4.0,-4.0), WOF_INTERPOLATE); A_SetPitch(pitch - (0.25 + frandom(0.2,-0.2)) * GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE); A_SetAngle(angle - (0.25 + frandom(0.2,-0.2)) * GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE); }
		FRGF C 1 Bright { A_WeaponOffset(22.0+frandom(2.0,-2.0), 32+22.0+frandom(2.0,-2.0), WOF_INTERPOLATE); }
		FRGF C 1 Bright
		FRGF A 0 A_JumpIf(!CountInv("FireblastCounter") && GetPlayerInput(INPUT_BUTTONS) & BT_RELOAD, "Reload")
		FRGF A 0 A_JumpIf(!CountInv("FireblastCounter"), "FireAnim2")
		Goto Reload+2
   Flash:
      TNT1 A 3 Bright A_Light2
      TNT1 A 4 Bright A_Light1
      TNT1 A 0 Bright A_Light0
      Stop
   StandardZoomRecoil:
		TNT1 A 1 A_ZoomFactor(0.97)
		TNT1 A 1 A_ZoomFactor(0.92)
		TNT1 A 1 A_ZoomFactor(0.91)
		TNT1 A 1 A_ZoomFactor(0.9)
		TNT1 A 1 A_ZoomFactor(0.915)
		TNT1 A 1 A_ZoomFactor(0.925)
		TNT1 A 1 A_ZoomFactor(0.95)
		TNT1 A 1 A_ZoomFactor(0.96)
		TNT1 A 1 A_ZoomFactor(0.975)
		TNT1 A 1 A_ZoomFactor(0.985)
		TNT1 A 1 A_ZoomFactor(0.995)
		TNT1 A 1 A_ZoomFactor(1.0)
		Stop
	RapidZoomRecoil:
		TNT1 A 1 A_ZoomFactor(0.99)
		TNT1 A 1 A_ZoomFactor(0.98)
		TNT1 A 1 A_ZoomFactor(0.995)
		TNT1 A 1 A_ZoomFactor(1.0)
		Stop
   }
}