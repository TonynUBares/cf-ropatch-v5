/*
CREDITS:
Gun sprites - Neccronixis
Projectile sprite - Meeeeee, what a surprise!
Sounds - Raven Software, ID Software, Epic Games, Croteam, People can Fly
Projectile - People can Fly
Impaler - Raven Software
Idea - Xaser
Pickup - ThingThing
Pump Hand - Eriance
*/

ACTOR VladCounter : Inventory { Inventory.MaxAmount 99 }

actor TepeschGiver : CustomInventory
{
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got the ''Tepesch'' impaler rifle! Just a weapon made by a mole"
	Obituary "%o was nailed by %k's ''Tepesch'' rifle. Ouch."
	States
	{
	Spawn:
		VLDG X 1 
		Loop
  Pickup:
	TNT1 A 0 A_JumpIfInventory("Tepesch",1,"JustAmmo")
  All:
	TNT1 A 0 A_GiveInventory("Tepesch",1)
	Stop
  JustAmmo:
	TNT1 A 0 A_GiveInventory("Boolet",90)
  Remove:
    TNT1 A 0
    Stop
	}
}

ACTOR Tepesch : RODoomWeapon 24510
{
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got the ''Tepesch'' impaler rifle! Just a weapon made by a mole"
	Obituary "%o was nailed by %k's ''Tepesch'' rifle. Ouch."
	Weapon.UpSound "Tepesch/Up"
	Weapon.AmmoType "Boolet"
	Weapon.AmmoGive 90
	Weapon.AmmoUse 3
	Weapon.AmmoType2 "Boolet"
	Weapon.AmmoUse2 40
	Weapon.SlotNumber 7
	Weapon.SlotPriority 29.0
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobSpeed 1.3
	Weapon.BobRangeY 0.6
	Weapon.BobRangeX 1.2
	+NOALERT
	+NOAUTOFIRE
	States
	{
	Spawn:
		VLDG X -1
		Stop
	Ready:
		VLDG A 0 A_JumpIfInventory("Zoomed",1,"ZoomedReady")
		VLDG A 1 A_WeaponReady(WRF_ALLOWZOOM)
		Loop
	Select:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,0,1,0)
	SelectLoop:
		VLDG A 0 A_TakeInventory("Zoomed")
		VLDG A 1 A_Raise(12)
		Wait
	Deselect:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,1,1,0)
	DeselectLoop:
		VLDG A 0 A_JumpIfInventory("Zoomed",1,"ZoomedDeselect")
		VLDG A 0 {
			A_ZoomFactor(1);
			A_StopSound(6);
			A_SetTranslucent(1,0);
		}
		VLDG A 1 A_Lower(12)
		Wait
	Fire:
		VLDG A 0 A_JumpIfInventory("Zoomed",1,"ZoomedFire")
		VLDF A 0 {
			A_AlertMonsters;
			A_GunFlash;
			A_Recoil(3.0);
			A_SetPitch(Pitch - 1.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 0.75*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_PlaySound("Tepesch/Fire",5);
			A_FireCustomMissile("VladStake",0,1,11,0,0,0);
			A_GiveInventory("BooletCasing",3);
		}
		VLDF A 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.98); } A_WeaponOffset(3,35,WOF_INTERPOLATE); }
		VLDF B 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.985); } A_WeaponOffset(7,39,WOF_INTERPOLATE); }
		VLDG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.995); } A_WeaponOffset(11,41,WOF_INTERPOLATE); }
		VLDG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(1.0); } A_WeaponOffset(12,42,WOF_INTERPOLATE); }
		VLDG A 1 A_WeaponOffset(10,42,WOF_INTERPOLATE)
		VLDG A 1 A_WeaponOffset(7,39,WOF_INTERPOLATE)
		VLDG A 1 A_WeaponOffset(4,36,WOF_INTERPOLATE)
		VLDG A 1 A_WeaponOffset(2,34,WOF_INTERPOLATE)
		VLDG A 0 A_WeaponOffset(0,32,WOF_INTERPOLATE)
		VLDG A 10 A_ReFire
		Goto Ready
	Hold:  
		VLDF A 0 A_JumpIfNoAmmo("Endo")
		VLDF A 0 {
			A_AlertMonsters;
			A_GunFlash;
			A_Recoil(1.0);
			A_SetPitch(Pitch-0.75*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle-0.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_PlaySound("Tepesch/Loop",6,1.0,1);
			A_FireCustomMissile("VladStake",frandom(0.75,-0.75),1,11,0,0,frandom(0.75,-0.75));
			A_GiveInventory("BooletCasing",3);
		}
		VLDF A 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.985); } A_WeaponOffset(3,35,WOF_INTERPOLATE); }
		VLDF B 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.995); } A_WeaponOffset(7,39,WOF_INTERPOLATE); }
		VLDG A 1 		{ if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(1.0); } A_WeaponOffset(11,41,WOF_INTERPOLATE); }
		VLDG A 1 A_WeaponOffset(7,39,WOF_INTERPOLATE)
		VLDG A 0 A_ReFire
	Endo:
		VLDF A 0 A_PlaySound("Tepesch/Stop",6)
		VLDG A 1 A_WeaponOffset(6,38,WOF_INTERPOLATE)
		VLDG A 1 A_WeaponOffset(4,36,WOF_INTERPOLATE)
		VLDG A 1 A_WeaponOffset(2,34,WOF_INTERPOLATE)
		VLDG A 1 A_WeaponOffset(1,33,WOF_INTERPOLATE)
		VLDG A 10 A_WeaponOffset(0,32,WOF_INTERPOLATE)
		Goto Ready
	Flash:
		TNT1 A 0 A_JumpIfInventory("Zoomed",1,3)
		TNT1 A 5 A_Light2
		TNT1 A 7 A_Light1
		Goto Lightdone
		TNT1 A 5 A_Light2
		TNT1 A 8 A_Light1
		Goto Lightdone

   ZoomedReady:
      VLDZ A 0 A_SetBlend("Red",0.1,3)
      VLDZ A 1 A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
      Goto Ready
   UnZoom:
      VLDZ A 0 A_PlaySound("Tepesch/ZoomOut",5)
      VLDZ A 0 A_TakeInventory("Zoomed")

      VLDZ A 1 { A_SetBlend("Black",0.3,3); A_SetTranslucent(0.9,0); }
      VLDZ A 1 { A_SetBlend("Black",0.4,3); A_SetTranslucent(0.92,0); }
      VLDZ A 1 { A_SetBlend("Black",0.45,3); A_SetTranslucent(0.94,0); }
      VLDZ A 1 { A_SetBlend("Black",0.6,3); A_SetTranslucent(0.96,0); A_ZoomFactor(1.0); }
      VLDZ A 1 { A_SetBlend("Black",0.75,3); A_SetTranslucent(0.98,0); }
      VLDZ A 1 { A_SetBlend("Black",0.9,3); A_SetTranslucent(1.0,0); }
	  
      VLDG A 16 A_SetBlend("Black",1.0,25)
      Goto Ready
  ZoomedDeselect:
      VLDZ A 0 A_PlaySound("Tepesch/ZoomOut",5)
      VLDZ A 0 A_TakeInventory("Zoomed")

      VLDZ A 1 { A_SetBlend("Black",0.3,3); A_SetTranslucent(0.9,0); }
      VLDZ A 1 { A_SetBlend("Black",0.4,3); A_SetTranslucent(0.92,0); }
      VLDZ A 1 { A_SetBlend("Black",0.45,3); A_SetTranslucent(0.94,0); }
      VLDZ A 1 { A_SetBlend("Black",0.6,3); A_SetTranslucent(0.96,0); A_ZoomFactor(1.0); }
      VLDZ A 1 { A_SetBlend("Black",0.75,3); A_SetTranslucent(0.98,0); }
      VLDZ A 1 { A_SetBlend("Black",0.9,3); A_SetTranslucent(1.0,0); }
      
      VLDG A 16 A_SetBlend("Black",1.0,25)
  ZoomedDeselectMain:
      VLDG A 1 A_Lower
      VLDG A 0 A_Lower
      Loop

	ZoomedFire:
		VLDZ A 1 Bright {
			A_GunFlash;
			A_AlertMonsters;
			A_PlaySound("Tepesch/Fire",5);
			A_FireCustomMissile("VladStake",0,1,0,0,0,0);
			A_GiveInventory("BooletCasing",3);
			A_SetBlend("Red",0.15,5);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.975); }
			A_SetPitch(Pitch-0.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		VLDZ A 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.985); } }
		VLDZ A 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.995); } }
		VLDZ A 1		{ if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(3.0); } }
		VLDZ AAAA 1 A_SetBlend("Red",0.1,3)
		VLDZ A 0 A_ReFire("ZoomedHold")
		VLDZ A 0 A_GiveInventory("VladCounter",10)
		Goto Charge
	ZoomedHold:
		VLDZ A 0 {
			A_GunFlash;
			A_AlertMonsters;
			A_GunFlash;
			A_PlaySound("Tepesch/Loop",6,1.0,1);
			A_FireCustomMissile("VladStake",frandom(0.5,-0.5),1,0,0,0,frandom(0.5,-0.5));
			A_GiveInventory("BooletCasing",3);
			A_SetBlend("Red",0.1,3);
		}
		VLDZ A 1 Bright {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.985); }
			A_SetPitch(Pitch-0.2*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		VLDZ A 1 Bright { A_SetBlend("Red",0.1,3); if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.99); } }
		VLDZ A 1 Bright { A_SetBlend("Red",0.1,3); if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.995); } }
		VLDZ A 1		{ A_SetBlend("Red",0.1,3); if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(3.0); } }
		VLDZ A 0 A_ReFire("ZoomedHold")
		VLDF A 0 {
			A_PlaySound("Tepesch/Stop",6);
			A_GiveInventory("VladCounter",13);
		}
		Goto Charge
	Zoom:
		VLDG A 0 A_JumpIfInventory("Zoomed",1,"UnZoom")
		VLDG A 0 {
			A_GiveInventory("Zoomed",1);
			A_PlaySound("Tepesch/ZoomIn",5);
		}
		VLDG A 1 A_SetBlend("Black",0.15,3)
		VLDG A 1 A_SetBlend("Black",0.3,3)
		VLDG A 1 A_SetBlend("Black",0.45,3)
		VLDG A 1 A_SetBlend("Black",0.6,3)
		VLDG A 1 A_SetBlend("Black",0.75,3)
		VLDG A 1 A_SetBlend("Black",0.9,3)
		VLDG A 1 A_SetBlend("Black",1.0,15)
		
		VLDZ A 1 { A_SetBlend("Black",0.9,3); A_SetTranslucent(0.98,0); A_ZoomFactor(3.00); }
		VLDZ A 1 { A_SetBlend("Black",0.85,3); A_SetTranslucent(0.96,0); }
		VLDZ A 1 { A_SetBlend("Black",0.75,3); A_SetTranslucent(0.84,0); }
		VLDZ A 1 { A_SetBlend("Black",0.65,3); A_SetTranslucent(0.82,0); }
		VLDZ A 1 { A_SetBlend("Black",0.55,3); A_SetTranslucent(0.81,0); }
		VLDZ A 1 { A_SetBlend("Black",0.45,3,"Red"); A_SetTranslucent(0.8,0); }
		
		VLDZ A 1 A_SetBlend("Red",0.15,3)
		VLDZ A 1 A_SetBlend("Red",0.14,3)
		VLDZ A 1 A_SetBlend("Red",0.13,3)
		VLDZ A 1 A_SetBlend("Red",0.12,3)
		VLDZ A 1 A_SetBlend("Red",0.11,3)
		VLDZ A 1 A_SetBlend("Red",0.1,3)
		
		VLDZ A 0 A_GiveInventory("VladCounter",5)
		Goto Charge
	Charge:
		VLDZ A 0 A_JumpIfInventory("VladCounter",1,"Looplet")
		Goto Ready
	Looplet:
		VLDZ A 1 A_SetBlend("Red",0.1,3)
		VLDZ A 0 A_TakeInventory("VladCounter",1)
		Goto Charge
   AltFire:
		VLDG A 0 A_JumpIfInventory("Zoomed",1,"ZoomedAltFire")
		VLDF A 1 Bright {
			A_AlertMonsters;
			A_GunFlash;
			A_Recoil(8.0);
			A_SetPitch(Pitch - 7.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 5.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_PlaySound("Tepesch/AltFire",5);
			A_PlaySound("Tepesch/AddAltFire",6);
			A_FireCustomMissile("VladSpikeProjectile",0,1,0,0,0,0);
			A_GiveInventory("BooletCasing",40);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.98); }
			A_WeaponOffset(3,35,WOF_INTERPOLATE);
		}
		VLDF B 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.97); } A_WeaponOffset(7,39,WOF_INTERPOLATE); }
		VLDG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.975); } A_WeaponOffset(11,43,WOF_INTERPOLATE); }
		VLDG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.98); } A_WeaponOffset(12,44,WOF_INTERPOLATE); }
		VLDG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.985); } A_WeaponOffset(11,43,WOF_INTERPOLATE); }
		VLDG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.99); } A_WeaponOffset(8,40,WOF_INTERPOLATE); }
		VLDG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.995); } A_WeaponOffset(4,36,WOF_INTERPOLATE); }
		VLDG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(1.0); } A_WeaponOffset(0,32,WOF_INTERPOLATE); }
		VLDG A 0 A_Overlay(-2,"CockTheGun")
		VLDG A 11
		VLDG A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE)
		VLDG A 1 A_WeaponOffset(1,34,WOF_INTERPOLATE)
		VLDG A 1 A_WeaponOffset(2,35,WOF_INTERPOLATE)
		VLDG A 1 A_WeaponOffset(3,36,WOF_INTERPOLATE)
		VLDG A 6 A_WeaponOffset(4,37,WOF_INTERPOLATE)
		VLDG A 1 A_WeaponOffset(3,36,WOF_INTERPOLATE)
		VLDG A 1 A_WeaponOffset(2,35,WOF_INTERPOLATE)
		VLDG A 1 A_WeaponOffset(1,34,WOF_INTERPOLATE)
		VLDG A 1 A_WeaponOffset(0,33,WOF_INTERPOLATE)
		VLDG A 5 A_WeaponOffset(0,32,WOF_INTERPOLATE)
		Goto Ready
		
	CockTheGun:
		VLDH A 1 A_OverlayOffset(OverlayID(),320,129)
		VLDH A 1 A_OverlayOffset(OverlayID(),315,118,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),310,108,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),305,99,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),303,93,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),295,86,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),288,81,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),279,78,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),272,77,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),263,79,WOF_INTERPOLATE)
		VLDH B 1 A_OverlayOffset(OverlayID(),263,91)
		VLDH B 1 A_OverlayOffset(OverlayID(),267,94,WOF_INTERPOLATE)
		VLDH B 1 A_OverlayOffset(OverlayID(),270,97,WOF_INTERPOLATE)
		VLDH B 1 A_OverlayOffset(OverlayID(),274,101,WOF_INTERPOLATE)
		VLDH B 1 A_OverlayOffset(OverlayID(),276,103,WOF_INTERPOLATE)
		VLDH B 1 A_OverlayOffset(OverlayID(),278,105,WOF_INTERPOLATE)
		
		VLDH B 3 A_OverlayOffset(OverlayID(),279,106,WOF_INTERPOLATE)
		
		VLDH B 1 A_OverlayOffset(OverlayID(),278,105,WOF_INTERPOLATE)
		VLDH B 1 A_OverlayOffset(OverlayID(),276,103,WOF_INTERPOLATE)
		VLDH B 1 A_OverlayOffset(OverlayID(),274,101,WOF_INTERPOLATE)
		VLDH B 1 A_OverlayOffset(OverlayID(),270,97,WOF_INTERPOLATE)
		VLDH B 1 A_OverlayOffset(OverlayID(),267,94,WOF_INTERPOLATE)
		VLDH B 1 A_OverlayOffset(OverlayID(),263,91,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),263,79)
		VLDH A 1 A_OverlayOffset(OverlayID(),272,77,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),279,78,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),288,81,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),295,86,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),303,93,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),305,99,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),310,108,WOF_INTERPOLATE)
		VLDH A 1 A_OverlayOffset(OverlayID(),315,118,WOF_INTERPOLATE)
		Stop
		
	ZoomedAltFire:
		VLDZ A 0 {
			A_GunFlash;
			A_Recoil(2.0);
			A_FireCustomMissile("VladSpikeProjectile",0,1); //A_FireBullets(1,0.5,1,0,"VladPuff",0)
			A_GiveInventory("BooletCasing",30);
			A_PlaySound("Tepesch/AltFire",5);
			A_PlaySound("Tepesch/AddAltFire",6);
			A_SetBlend("Red",0.3,7);
		}
		VLDZ A 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.95); } }
		VLDZ A 0 A_SetPitch(Pitch-6.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE)
		
		VLDZ A 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.97); } }
		VLDZ A 1 Bright { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.99); } }
		VLDZ A 1 		{ if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.995); } }
		VLDZ A 1 		{ if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.9975); } }
		VLDZ A 1 		{ if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(3.0); } }
		VLDZ A 0 A_GiveInventory("VladCounter",40)
		Goto Charge
   }
}

