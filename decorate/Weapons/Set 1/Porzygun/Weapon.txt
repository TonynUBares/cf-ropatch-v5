/*
CREDITS:

Sprites - NeoWorm, Bethesda (TES: Daggerfall, I think)
Hands by Marty Kirra and Eriance\Amuscaria.
Edits on both done by me.
Projectile from Hexen, so it goes to Raven Soft.

Sounds:
Swing sounds - Blizzard (Diablo 1)
Up sound, Impacts except flesh ones - Firefly Studios (Stronghold)
Flesh impacts - Hmmmmmm. Wish I'd remember?
Grappling attack - Blizzard (Diablo 2)
WHEEEEE - Dumb Mozilla advert.
*/

/*
So now this weapon has it's own, actual name, in case of someone having a genius
idea of naming his enemy or some actor like MorningStar or Mace or whatver,
in case of things going full conflict.
*/

//Triggers
ACTOR MaceSide : Inventory { Inventory.MaxAmount 1 }
ACTOR MaceReloading : Inventory { Inventory.MaxAmount 1 }
ACTOR MaceCounter : Inventory { Inventory.MaxAmount 16 }


Actor PorzygunGiver : CustomInventory
{
  Scale 0.8
  Inventory.Amount 1
  Inventory.MaxAmount 0
  Inventory.InterHubAmount 0
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You've got the ''Porzygun'' morning star! Wait.....why tf theres a medieval weapon here?"
	Obituary "%k applied mace-on directly on %o's forehead."
  Tag "Porzygun"
  States
  {
  Spawn:
	MACG X 1
	Loop
  Pickup:
	TNT1 A 0 A_JumpIfInventory("Porzygun",1,"Remove")
  All:
	TNT1 A 0 A_GiveInventory("Porzygun",1)
	Stop
  Remove:
    TNT1 A 0
    Stop
  }
}

//Weapon
ACTOR Porzygun : ROWeapon 24500
{
	//$Category Weapons
	//$Title Porzygun
	//$Sprite MACEX0

	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You've got the ''Porzygun'' morning star! Wait.....why tf theres a medieval weapon here?"
	Obituary "%k applied mace-on directly on %o's forehead."
	Weapon.Kickback 0
   	Weapon.UpSound "Porzygun/SwingMain"
   	Weapon.SelectionOrder 7 
	Weapon.SlotNumber 1
	Weapon.SlotPriority 29.0
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobSpeed 1.6
	Weapon.BobRangeY 0.7
	Weapon.BobRangeX 0.9
	Tag "Porzygun"
	+NOALERT
	States
	{
	Spawn:
		MACG X -1
		Stop
	Select:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,0,1,0)
	SelectLoop:
		TNT1 A 0 SetPlayerProperty(0,1,2)
		TNT1 A 0 A_Raise
		Wait
	Deselect:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,1,1,0)
	DeselectLoop:
		TNT1 A 0 {
			A_ZoomFactor(1.0);
			A_StopSound(6);
			SetPlayerProperty(0,0,2);
		}
		TNT1 A 9   A_Overlay(2,"DeselectAnim") //did it like this, cause I get insanely weird view jittering with WeaponOffset. And it's dumb.
		Goto DeselectMain
	DeselectAnim:
		MACE A 1   A_OverlayOffset(2, 167.0, 9.0, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 176.0, -1.0, WOF_INTERPOLATE)
		MACE B 1   A_OverlayOffset(2, 187.0, 5.0, WOF_INTERPOLATE)
		MACE B 1   A_OverlayOffset(2, 210.0, -3.0, WOF_INTERPOLATE)
		MACE B 1   A_OverlayOffset(2, 226.0, 2.0, WOF_INTERPOLATE)
		MACE B 1   A_OverlayOffset(2, 248.0, 12.0, WOF_INTERPOLATE)
		MACE C 1   A_OverlayOffset(2, 253.0, 59.0, WOF_INTERPOLATE)
		MACE C 1   A_OverlayOffset(2, 261.0, 85.0, WOF_INTERPOLATE)
		MACE C 1   A_OverlayOffset(2, 278.0, 115.0, WOF_INTERPOLATE)
		Stop
	DeselectMain:
		TNT1 A 0 A_Lower
		Loop
	Ready:
		TNT1 A 13   A_Overlay(2,"ReadyAnim")
		Goto Return
	ReadyAnim:
		MACE F 1   A_OverlayOffset(2, -104.0, 42.0, WOF_INTERPOLATE)
		MACE F 1   A_OverlayOffset(2, -44.0, 17.0, WOF_INTERPOLATE)
		MACE F 1   A_OverlayOffset(2, 15.0, 8.0, WOF_INTERPOLATE)
		MACE E 1   A_OverlayOffset(2, 105.0, -94.0, WOF_INTERPOLATE)
		MACE E 1   A_OverlayOffset(2, 172.0, -86.0, WOF_INTERPOLATE)
		MACE D 1   A_OverlayOffset(2, 220.0, -54.0, WOF_INTERPOLATE)
		MACE D 1   A_OverlayOffset(2, 256.0, -19.0, WOF_INTERPOLATE)
		Stop
	Return:
		TNT1 A 9   A_Overlay(2,"ReturnAnim")
		Goto ReadyMain
	ReturnAnim:
		MACE C 1   A_OverlayOffset(2, 278.0, 115.0, WOF_INTERPOLATE)
		MACE C 1   A_OverlayOffset(2, 261.0, 85.0, WOF_INTERPOLATE)
		MACE C 1   A_OverlayOffset(2, 253.0, 59.0, WOF_INTERPOLATE)
		MACE B 1   A_OverlayOffset(2, 248.0, 12.0, WOF_INTERPOLATE)
		MACE B 1   A_OverlayOffset(2, 226.0, 2.0, WOF_INTERPOLATE)
		MACE B 1   A_OverlayOffset(2, 210.0, -3.0, WOF_INTERPOLATE)
		MACE B 1   A_OverlayOffset(2, 187.0, 5.0, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 176.0, -1.0, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 167.0, 9.0, WOF_INTERPOLATE)
		Stop
	ReadyMain:
		TNT1 A 1   { A_WeaponReady(WRF_ALLOWRELOAD); A_Overlay(2,"ReadyMainAnim"); }
		Loop
	ReadyMainAnim:
		MACE A 1   A_OverlayOffset(2, 163.0, 13.0)
		Loop
			
	Fire:
		TNT1 A 9   A_Overlay(2,"FireAnim")
		Goto RightSwing2
	FireAnim:
		MACE A 1   A_OverlayOffset(2, 167, 9, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 176, -1, WOF_INTERPOLATE)
		MACE B 1   A_OverlayOffset(2, 187, 5, WOF_INTERPOLATE)
		MACE B 1   A_OverlayOffset(2, 210, -3, WOF_INTERPOLATE)
		MACE B 1   A_OverlayOffset(2, 226, 2, WOF_INTERPOLATE)
		MACE B 1   A_OverlayOffset(2, 248, 12, WOF_INTERPOLATE)
		MACE C 1   A_OverlayOffset(2, 253, 59, WOF_INTERPOLATE)
		MACE C 1   A_OverlayOffset(2, 261, 85, WOF_INTERPOLATE)
		MACE C 1   A_OverlayOffset(2, 278, 115, WOF_INTERPOLATE)
		Stop
	SwingRepeat:
		TNT1 A 0 A_JumpIf(CountInv("MaceSide") > 0, "LeftSwing")
		TNT1 A 0 A_Jump(256,"RightSwing1","RightSwing2")
	LeftSwing:
		TNT1 A 0 A_Jump(256,"LeftSwing1","LeftSwing2")
	RightSwing1:
		TNT1 A 4 {
			A_GiveInventory("MaceSide",1);
			A_PlaySound("Porzygun/Swing",5);
			A_PlaySound("Weapons/MaceSwingMain",1);
			A_Overlay(2,"RightSwing1Anim");
		}
		TNT1 A 8 A_CustomPunch(350,0,0,"PorzygunPuff",175+random(15,-10))
		TNT1 A 0 A_ReFire("SwingRepeat")
		Goto Return
	RightSwing1Anim:
		MACE G 1   A_OverlayOffset(2, 160, 117, WOF_INTERPOLATE)
		MACE G 1   A_OverlayOffset(2, 95, 86, WOF_INTERPOLATE)
		MACE H 1   A_OverlayOffset(2, 43, 88, WOF_INTERPOLATE)
		MACE H 1   A_OverlayOffset(2, -72, 65, WOF_INTERPOLATE)
		MACE I 1   A_OverlayOffset(2, -138, 62, WOF_INTERPOLATE)
		MACE I 1   A_OverlayOffset(2, -221, 76, WOF_INTERPOLATE)
		MACE I 1   A_OverlayOffset(2, -292, 102, WOF_INTERPOLATE)
		MACE I 1   A_OverlayOffset(2, -355, 120, WOF_INTERPOLATE)
		Stop
	RightSwing2:
		TNT1 A 4 {
			A_GiveInventory("MaceSide",1);
			A_PlaySound("Porzygun/Swing",5);
			A_PlaySound("Weapons/MaceSwingMain",1);
			A_Overlay(2,"RightSwing2Anim");
		}
		TNT1 A 8 A_CustomPunch(350,0,0,"PorzygunPuff",175+random(15,-10))
		TNT1 A 0 A_ReFire("SwingRepeat")
		Goto Return
	RightSwing2Anim:
		MACE D 1   A_OverlayOffset(2, 269, -101, WOF_INTERPOLATE)
		MACE D 1   A_OverlayOffset(2, 208, -72, WOF_INTERPOLATE)
		MACE E 1   A_OverlayOffset(2, 190, -40, WOF_INTERPOLATE)
		MACE E 1   A_OverlayOffset(2, 143, -6, WOF_INTERPOLATE)
		MACE F 1   A_OverlayOffset(2, 142, 60, WOF_INTERPOLATE)
		MACE F 1   A_OverlayOffset(2, 88, 79, WOF_INTERPOLATE)
		MACE F 1   A_OverlayOffset(2, 48, 114, WOF_INTERPOLATE)
		MACE F 1   A_OverlayOffset(2, 7, 161, WOF_INTERPOLATE)
		Stop
		
	LeftSwing1:
		 TNT1 A 4 {
			A_TakeInventory("MaceSide",1);
			A_PlaySound("Porzygun/Swing",5);
			A_PlaySound("Weapons/MaceSwingMain",1);
			A_Overlay(2,"LeftSwing1Anim");
		}
		TNT1 A 8 A_CustomPunch(350,0,0,"PorzygunPuff",175+random(15,-10))
		TNT1 A 0 A_ReFire("SwingRepeat")
		Goto Return
	LeftSwing1Anim:
		MACE M 1   A_OverlayOffset(2, -361, 106, WOF_INTERPOLATE)
		MACE M 1   A_OverlayOffset(2, -251, 80, WOF_INTERPOLATE)
		MACE N 1   A_OverlayOffset(2, -210, 6, WOF_INTERPOLATE)
		MACE N 1   A_OverlayOffset(2, -57, 59, WOF_INTERPOLATE)
		MACE O 1   A_OverlayOffset(2, 103, 53, WOF_INTERPOLATE)
		MACE O 1   A_OverlayOffset(2, 194, 63, WOF_INTERPOLATE)
		MACE O 1   A_OverlayOffset(2, 276, 85, WOF_INTERPOLATE)
		MACE O 1   A_OverlayOffset(2, 347, 99, WOF_INTERPOLATE)
		Stop
		
	LeftSwing2:
		 TNT1 A 4 {
			A_TakeInventory("MaceSide",1);
			A_PlaySound("Porzygun/Swing",5);
			A_PlaySound("Weapons/MaceSwingMain",1);
			A_Overlay(2,"LeftSwing2Anim");
		}
		TNT1 A 8 A_CustomPunch(350,0,0,"PorzygunPuff",175+random(15,-10))
		TNT1 A 0 A_ReFire("SwingRepeat")
		Goto Return
	LeftSwing2Anim:
		MACE J 1 A_OverlayOffset(2, -173, -108, WOF_INTERPOLATE)
		MACE J 1 A_OverlayOffset(2, -123, -96, WOF_INTERPOLATE)
		MACE K 1 A_OverlayOffset(2, -85, -96, WOF_INTERPOLATE)
		MACE K 1 A_OverlayOffset(2, -47, -68, WOF_INTERPOLATE)
		MACE L 1 A_OverlayOffset(2, -175, 55, WOF_INTERPOLATE)
		MACE L 1 A_OverlayOffset(2, -132, 102, WOF_INTERPOLATE)
		MACE L 1 A_OverlayOffset(2, -103, 150, WOF_INTERPOLATE)
		MACE L 1 A_OverlayOffset(2, -82, 202, WOF_INTERPOLATE)
		Stop
	Reload:
		TNT1 A 0   A_JumpIfInventory("MaceReloading",1,"ReloadGoOn")
		TNT1 A 9   A_Overlay(2,"FireAnim")
		TNT1 A 3
		{
			A_GiveInventory("PorzygunResistor",1);
			A_PlaySound("Porzygun/Yay",5);
			A_PlaySound("Porzygun/Loop",6,1.0,1);
		}
		TNT1 A 3   A_Overlay(2,"ReloadStartAnim")
		Goto ReloadGoOn
	ReloadStartAnim:
		MACE G 1 { A_OverlayOffset(2, 160, 107, WOF_INTERPOLATE); A_ZoomFactor(0.98); }
		MACE G 1 { A_OverlayOffset(2, 95, 76, WOF_INTERPOLATE); A_ZoomFactor(0.94); }
		MACE H 1 { A_OverlayOffset(2, 43, 78, WOF_INTERPOLATE); A_ZoomFactor(0.9); }
		Stop
	ReloadGoOn:
		TNT1 A 0 A_GiveInventory("MaceCounter",11)
		TNT1 A 0 A_GiveInventory("MaceReloading",1)
	ReloadMain:
		TNT1 A 1
		{
			A_Overlay(2,"ReloadFrameAnim");
			A_GiveInventory("PorzygunResistor",1);
			A_TakeInventory("MaceCounter",1);
			A_SetAngle(Angle+30.0,SPF_INTERPOLATE);
			A_Explode(50,128,0);
			A_ChangeVelocity(frandom(2.0,-2.0),frandom(2.0,-2.0), 0,CVF_RELATIVE);
			A_CustomPunch(150,0,0,"PorzygunPuff",150+random(25,-10));
		}
		"####" "#" 0 A_JumpIfInventory("MaceCounter",1,"ReloadMain")
	ReloadEnd:
		TNT1 A 0
		{
			A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH);
			A_ZoomFactor(1.0);
			A_Overlay(2,"ReloadEndAnim");
		}
		TNT1 A 1 A_SetAngle(Angle+15.0,SPF_INTERPOLATE)
		TNT1 A 1 A_SetAngle(Angle+7.5,SPF_INTERPOLATE)
		TNT1 A 1 {
			A_SetAngle(Angle+2.5,SPF_INTERPOLATE);
			A_StopSound(6);
			A_TakeInventory("MaceReloading",1);
		}
		TNT1 AAAAA 1 A_SetAngle(Angle+2.0,SPF_INTERPOLATE)
		goto Return
	ReloadFrameAnim:
		TNT1 A 0 A_OverlayOffset(2, -72.0 + frandom(1.0,-1.0), 65.0 + frandom(1.0,-1.0))
		TNT1 A 0 A_Jump(256,1,2,3,4)
	ReloadFrame:
		MACR A 1
		Stop
		MACR B 1
		Stop
		MACR C 1
		Stop
		MACR D 1
		Stop
	ReloadEndAnim:
		MACE I 1  A_OverlayOffset(2, -221, 66, WOF_INTERPOLATE)
		MACE I 1  A_OverlayOffset(2, -292, 92, WOF_INTERPOLATE)
		MACE I 1  A_OverlayOffset(2, -355, 110, WOF_INTERPOLATE)
		Stop
	/*RotatorLeft:
		MACL A 1   A_OverlayOffset(2, -58, 79)
		MACL B 1   A_OverlayOffset(2, -57, 79)
		MACL C 1   A_OverlayOffset(2, -61, 79)
		TNT1 A 1   A_OverlayOffset(2, -59, 79)
		Loop*/

	AltFire:
		TNT1 A 15 { 
			A_Overlay(2,"AltFireAnim");
			A_Overlay(-3,"Handjob");
			A_PlaySound("Porzygun/Pull",6);
		}
		MACE A 0 A_ReFire("RightSwing2")
		Goto ReadyMain
	AltFireAnim:
		MACE A 1   A_OverlayOffset(2, 163, 13, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 166, 17, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 173, 27, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 177, 44, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 180, 53, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 181, 57, WOF_INTERPOLATE)
		MACE A 3 { A_OverlayOffset(2, 181, 60, WOF_INTERPOLATE); A_FireCustomMissile("PorzygunPull",0,0); }
		MACE A 1   A_OverlayOffset(2, 181, 57, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 180, 53, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 177, 44, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 173, 27, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 166, 17, WOF_INTERPOLATE)
		MACE A 1   A_OverlayOffset(2, 163, 13, WOF_INTERPOLATE)
		Stop
	Handjob:
		MACH A 1   A_OverlayOffset(-3, -67, 190, WOF_INTERPOLATE)
		MACH A 1   A_OverlayOffset(-3, -44, 141, WOF_INTERPOLATE)
		MACH A 1   A_OverlayOffset(-3, -23, 95, WOF_INTERPOLATE)
		MACH A 1   A_OverlayOffset(-3, -16, 78, WOF_INTERPOLATE)
		MACH A 1   A_OverlayOffset(-3, -12, 74, WOF_INTERPOLATE)
		MACH A 1   A_OverlayOffset(-3, -9, 70, WOF_INTERPOLATE)
		MACH A 1   A_OverlayOffset(-3, -7, 68, WOF_INTERPOLATE)
		MACH A 1   A_OverlayOffset(-3, -6, 67, WOF_INTERPOLATE)
		MACH A 1   A_OverlayOffset(-3, -6, 71, WOF_INTERPOLATE)
		MACH A 1   A_OverlayOffset(-3, -5, 85, WOF_INTERPOLATE)
		MACH A 1   A_OverlayOffset(-3, -10, 110, WOF_INTERPOLATE)
		MACH A 1   A_OverlayOffset(-3, -26, 142, WOF_INTERPOLATE)
		MACH A 1   A_OverlayOffset(-3, -45, 173, WOF_INTERPOLATE)
		Stop
	}
}

actor PorzygunResistor : PowerupGiver
{
	Inventory.Amount 1
	Powerup.Duration 35
	Powerup.Type "PorzygunResistance"
	Powerup.Color "None" 0.0
	Inventory.PickupMessage "SPIN TO WIN!"
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
}

actor PowerPorzygunResistance : PowerProtection
{
	DamageFactor "Normal", 0.2
}