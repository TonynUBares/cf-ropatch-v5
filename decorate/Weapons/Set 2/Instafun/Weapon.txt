/*
CREDITS:
INFO - Frankensprite - a sprite made of parts of other sprites.
Just like frankenstein is a mashup of other people's limbs. Crazy, eh?

Sprites:
Frankensprite - Ryan Cordell, I hope. Bits from 3DRealms work. (Shadow Warrior)
Smoke, ring - Smoke is definitely from Doom 3... not sure about the ring
Logan MTM - Projectile ring sprite (FUNPA0)

Sounds:
Up - Epic Games (UT2004)
Fire - sounds like it's Quake 2, but it ain't a one... not sure if edited, or from some other game.
Add - ID (Quake 4)
Fly - Valve (Half-Life 2)
Hit - ?
Zoom in, Zoom out - ID (ET:QW)
*/

ACTOR InstafunFireCounter : Inventory { Inventory.MaxAmount 2 }
ACTOR MagnetCooldown : Inventory { Inventory.MaxAmount 140 }


actor InstafunGiver : CustomInventory
{
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Mumbling in joy  at ''Instafuneral'' tri-shockrifle! No need for ceremonials - your death will be swift, and with style. (7)" //feel the "Allo Allo" reference!
	Obituary "%o's funeral was executed shockingly fast by %k."
	States
	{
	Spawn:
		FUNG X 1 
		Loop
  Pickup:
	TNT1 A 0 A_JumpIfInventory("Instafun",1,"JustAmmo")
  All:
	TNT1 A 0 A_GiveInventory("Instafun",1)
	Stop
  JustAmmo:
	TNT1 A 0 A_GiveInventory("IonAmmo",12)
  Remove:
    TNT1 A 0
    Stop
	}
}

ACTOR Instafun : DoomWeapon 24524
{
	//$Category Weapons
	//$Title Instafuneral
	//$Sprite FUNGX0
	Tag "Instafuneral"
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Mumbling in joy  at ''Instafuneral'' tri-shockrifle! No need for ceremonials - your death will be swift, and with style. (7)" //feel the "Allo Allo" reference!
	Obituary "%o's funeral was executed shockingly fast by %k."
	Weapon.UpSound "Instafun/Up"
	Weapon.SelectionOrder 2
	Weapon.AmmoType "IonAmmo"
	Weapon.AmmoGive 12
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "IonAmmo"
	Weapon.AmmoUse2 5
	Weapon.Kickback 70
	Weapon.SlotNumber 7
	Weapon.SlotPriority 25.0
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobSpeed 1.4
	Weapon.BobRangeY 0.6
	Weapon.BobRangeX 1.2
	Scale 0.85
	+NOALERT
	+NOAUTOFIRE
	States
	{
	Spawn:
		FUNG X -1
		Stop
	DeadLowered:
		TNT1 A -1
		Stop
	Ready:
		FUNG A 0 A_JumpIf(CountInv("Zoomed"),"ZoomedReady")
		FUNG A 1 A_WeaponReady(WRF_ALLOWZOOM)
		Loop
	ZoomedReady:
		FUNZ A 0 A_SetBlend("Green",0.025,3)
		FUNZ A 1 A_WeaponReady(WRF_NOBOB|WRF_ALLOWZOOM)
		Goto Ready
	Select:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,0,1,0)
	SelectLoop:
		FUNF A 0 {
			A_Overlay(3,"MagnetMonitor");
			A_TakeInventory("Zoomed",999);
			A_TakeInventory("InstafunFireCounter",2);
		}
		FUNG A 1 A_Raise(12)
		Wait
	Deselect:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,1,1,0)
	DeselectLoop:
		FUNG A 0 A_JumpIf(CountInv("Zoomed"),"UnZoom")
		FUNG A 0 {
			A_ZoomFactor(1);
			A_SetTranslucent(1,0);
		}
		FUNG A 1 A_Lower(12)
		Wait
	MagnetMonitor:
		FUNG B 1 Bright A_JumpIf(CountInv("MagnetCooldown"),1)
		Loop
		TNT1 A 1
		Loop
	Fire:
		FUNG A 0 A_JumpIfInventory("Zoomed",1,"ZoomedFire")
		FUNF A 0 {
			A_Overlay(2,"ZippityZap");
			A_AlertMonsters;
			A_Gunflash;
			A_Recoil(1.0);
			A_PlaySound("Instafun/AddFire",1,0.7);
		}
		FUNG A 0 A_JumpIf(CountInv("InstafunFireCounter") <= 0,"FireBarrel1")
		FUNG A 0 A_JumpIf(CountInv("InstafunFireCounter") == 1,"FireBarrel2")
		FUNG A 0 A_JumpIf(CountInv("InstafunFireCounter") >= 2,"FireBarrel3")
	FireBarrel1:
		FN1F A 0 {
			A_GiveInventory("InstafunFireCounter",1);
			A_PlaySound("Instafun/Fire",5,0.7);
			A_FireCustomMissile("InstafunProjectile",2,1,5,-1);
		}
		Goto FireBarrelMain
	FireBarrelMain:
		"####" A 1 Bright {
			A_WeaponOffset(3,32+9,WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.99); }
			A_SetAngle(angle-0.15*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			A_SetPitch(pitch-0.25*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
		}
		"####" B 1 Bright {
			A_WeaponOffset(5,32+13,WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.98); }
			A_SetAngle(angle-0.05*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			A_SetPitch(pitch-0.1*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
		}
		"####" C 1 {
			A_WeaponOffset(7,32+17,WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.97); }
			A_SetPitch (pitch-0.1*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
		}
		FUNG A 1 {
			A_WeaponOffset(8,32+19,WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.975); }
		}
		FUNG A 1 {
			A_WeaponOffset(7,32+17,WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.98); }
		}
		FUNG A 0 A_ReFire
		Goto FireEnd
	FireBarrel2:
		FN2F A 0 {
			A_GiveInventory("InstafunFireCounter",1);
			A_PlaySound("Instafun/Fire",6,0.7);
			A_FireCustomMissile("InstafunProjectile",0,1,10,-1);
		}
		Goto FireBarrelMain
   FireBarrel3:
		FN3F A 0 {
			A_TakeInventory("InstafunFireCounter",2);
			A_PlaySound("Instafun/Fire",4,0.7);
			A_FireCustomMissile("InstafunProjectile",-2,1,15,-1);
		}
		Goto FireBarrelMain
	FireEnd:
		FUNG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.97); } A_WeaponOffset(5,32+14,WOF_INTERPOLATE); }
		FUNG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.98); } A_WeaponOffset(4,32+11,WOF_INTERPOLATE); }
		FUNG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.985); } A_WeaponOffset(3,32+8,WOF_INTERPOLATE); }
		FUNG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.99); } A_WeaponOffset(2,32+5,WOF_INTERPOLATE); }
		FUNG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.995); } A_WeaponOffset(1,32+3,WOF_INTERPOLATE); }
		FUNG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(1.0); } A_WeaponOffset(0,32,WOF_INTERPOLATE); }
		FUNG A 0 A_ReFire
		Goto Ready
		
	Flash:
		TNT1 A 2 A_Light2
		TNT1 A 5 A_Light1
		TNT1 A 0 A_Light0
		Stop
   ZippityZap:
       FUNL ABCD 1 Bright
       Stop
  
	AltFire:
		FUNZ A 0 A_JumpIf(CountInv("MagnetCooldown"),"Ready")
		FUNG A 0 A_JumpIf(CountInv("Zoomed"),"ZoomedAltFire")
		FUNF A 0 {
			A_Recoil(20);
			A_Gunflash;
			A_AlertMonsters;
			A_GiveInventory("MagnetCooldown",140);
			A_SetBlend("Cyan",0.8,25);
			A_PlaySound("Instafun/AddFire",6,0.7);
			A_PlaySound("Instafun/MagnetFire",5,0.9);
			A_FireCustomMissile("MagnetProjectile",0,1,10,-1);
			//A_PlaySound("Instafun/Fire",0);
			//A_FireCustomMissile("InstafunProjectile",2,1,5);
			//A_FireCustomMissile("InstafunProjectile",0,0,10);
			//A_FireCustomMissile("InstafunProjectile",-2,0,15);
		}
		FNAF A 1 Bright {
			A_WeaponOffset(3,32+9,WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.96); }
			A_SetAngle(angle-4.0*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			A_SetPitch(pitch-4.0*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
		}
		FNAF B 1 Bright {
			A_WeaponOffset(5,32+13,WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.95); }
			A_SetAngle(angle-2.0*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			A_SetPitch(pitch-2.0*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
		}
		FNAF C 1 {
			A_WeaponOffset(7,32+17,WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.945); }
			A_SetAngle(angle-0.5*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			A_SetPitch(pitch-0.5*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
		}
		FUNG A 1 {
			A_WeaponOffset(8,32+19,WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.95); }
			A_SetAngle(angle-0.25*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			A_SetPitch(pitch-0.25*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
		}
		FUNG A 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.96); }
			A_SetAngle(angle-0.15*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			A_SetPitch(pitch-0.15*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
		}
		FUNG A 1 {
			A_WeaponOffset(7,32+17,WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.97); }
			A_SetAngle(angle-0.1*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			A_SetPitch(pitch-0.1*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
		}
		FUNG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.98); } A_WeaponOffset(5,32+14,WOF_INTERPOLATE); }
		FUNG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.985); } A_WeaponOffset(3,32+8,WOF_INTERPOLATE); }
		FUNG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.99); } A_WeaponOffset(2,32+5,WOF_INTERPOLATE); }
		FUNG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.995); } A_WeaponOffset(1,32+3,WOF_INTERPOLATE); }
		FUNG A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(1.0); } A_WeaponOffset(0,32,WOF_INTERPOLATE); }
		Goto Ready
		
   Zoom:
		FUNG A 0 A_JumpIfInventory("Zoomed",1,"UnZoom")
		FUNG A 0 {
			A_GiveInventory("Zoomed",1);
			A_PlaySound("Instafun/ZoomIn",5,0.7);
		}
		FUNG A 1 A_SetBlend("Black",0.15,3)
		FUNG A 1 A_SetBlend("Black",0.3,3)
		FUNG A 1 A_SetBlend("Black",0.45,3)
		FUNG A 1 A_SetBlend("Black",0.6,3)
		FUNG A 1 A_SetBlend("Black",0.75,3)
		FUNG A 1 A_SetBlend("Black",0.9,3)
		FUNG A 1 A_SetBlend("Black",1.0,15)
		FUNZ B 1 {
			A_Overlay(3,"None");
			A_ZoomFactor(3.00);
			A_SetBlend("Black",0.9,3);
			A_SetTranslucent(0.88,0);
		}
		FUNZ B 1 {
			A_SetBlend("Black",0.85,3);
			A_SetTranslucent(0.86,0);
		}
		FUNZ B 1 {
			A_SetBlend("Black",0.85,3);
			A_SetTranslucent(0.84,0);
		}
		FUNZ B 1 {
			A_SetBlend("Black",0.65,3);
			A_SetTranslucent(0.82,0);
		}
		FUNZ B 1 {
			A_SetBlend("Black",0.55,3);
			A_SetTranslucent(0.81,0);
		}
		FUNZ B 1 {
			A_SetBlend("Black",0.45,3);
			A_SetTranslucent(0.8,0);
		}
		FUNZ B 1 A_SetBlend("Black",0.4,3)
		FUNZ B 1 A_SetBlend("Black",0.36,3)
		FUNZ B 1 A_SetBlend("Black",0.34,3)
		FUNZ B 1 A_SetBlend("Black",0.33,3)
		FUNZ B 1 A_SetBlend("Black",0.32,3)
		FUNZ B 1 A_SetBlend("Black",0.31,3)
		Goto ZoomedReady
   UnZoom:
		FUNZ A 0 {
			A_PlaySound("Instafun/ZoomOut",5,0.7);
			A_TakeInventory("Zoomed",1);
		}
		FUNZ B 1 { A_SetBlend("Black",0.35,3); A_SetTranslucent(0.9,0); }
		FUNZ B 1 { A_SetBlend("Black",0.4,3); A_SetTranslucent(0.92,0); }
		FUNZ B 1 { A_SetBlend("Black",0.45,3); A_SetTranslucent(0.94,0); A_ZoomFactor(1.0); }
		FUNZ B 1 { A_SetBlend("Black",0.6,3); A_SetTranslucent(0.96,0); }
		FUNZ B 1 { A_SetBlend("Black",0.75,3); A_SetTranslucent(0.98,0); }
		FUNZ B 1 { A_SetBlend("Black",0.9,3); A_SetTranslucent(1.0,0); }
		FUNG A 16 { A_Overlay(3,"MagnetMonitor"); A_SetBlend("Black",1.1,25); }
		Goto Ready

	ZoomedFire:
		FUNG A 0 A_JumpIf(CountInv("InstafunFireCounter") <= 0,"ZoomedFireBarrel1")
		FUNG A 0 A_JumpIf(CountInv("InstafunFireCounter") == 1,"ZoomedFireBarrel2")
		FUNG A 0 A_JumpIf(CountInv("InstafunFireCounter") >= 2,"ZoomedFireBarrel3")
	ZoomedFireBarrel1:
		FUNZ A 0 {
			A_GiveInventory("InstafunFireCounter",1);
			A_PlaySound("Instafun/Fire",5,0.7);
			A_FireCustomMissile("InstafunProjectile",0,1,-8);
		}
		Goto ZoomedFireMain
	ZoomedFireBarrel2:
		FUNZ A 0 {
			A_GiveInventory("InstafunFireCounter",1);
			A_PlaySound("Instafun/Fire",6,0.7);
			A_FireCustomMissile("InstafunProjectile",0,1,-4);
		}
		Goto ZoomedFireMain
	ZoomedFireBarrel3:
		FUNZ A 0 {
			A_TakeInventory("InstafunFireCounter",2);
			A_PlaySound("Instafun/Fire",4,0.7);
			A_FireCustomMissile("InstafunProjectile",0,1,-2);
		}
		Goto ZoomedFireMain
	ZoomedFireMain:
		FUNZ B 1 Bright {
			A_AlertMonsters;
			A_PlaySound("Instafun/AddFire",1,0.8);
			A_SetPitch(pitch-0.25*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			A_SetBlend("Blue",0.1,25);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.99); }
		}
		FUNZ B 1 Bright {
			A_SetPitch(pitch-0.25*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.95); }
		}
		FUNZ B 1 {
			A_SetPitch(pitch+0.25*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.925); }
		}
		FUNZ B 1 {
			A_SetPitch(pitch+0.15*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.94); }
		}
		FUNZ B 1 {
			A_SetPitch(pitch+0.1*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.95); }
		}
		FUNZ A 0 A_ReFire
		FUNZ B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.96); }
			A_SetBlend("Green",0.025,3);
		}
		FUNZ B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.97); }
			A_SetBlend("Green",0.025,3);
		}
		FUNZ B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.98); }
			A_SetBlend("Green",0.025,3);
		}
		FUNZ B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.99); }
			A_SetBlend("Green",0.025,3);
		}
		FUNZ B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.995); }
			A_SetBlend("Green",0.025,3);
		}
		FUNZ B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(3.0); }
			A_SetBlend("Green",0.025,3);
		}
		FUNZ A 0 A_ReFire
		Goto Ready
		
   ZoomedAltFire:
		FUNZ A 0 A_JumpIfInventory("MagnetCooldown",1,"ZoomedReady")
		FUNZ A 0 {
			A_GiveInventory("MagnetCooldown",105);
			A_PlaySound("Instafun/AddFire",6,0.7);
			A_PlaySound("Instafun/MagnetFire",5,0.9);
			A_FireCustomMissile("MagnetProjectile",0,1,-4);
			//A_PlaySound("Instafun/Fire",0);
			//A_FireCustomMissile("InstafunProjectile",-0.5,1,-8,);
			//A_FireCustomMissile("InstafunProjectile",0,0,-4);
			//A_FireCustomMissile("InstafunProjectile",0.5,0,-2);
		}
		FUNZ B 1 Bright {
			A_SetBlend("Blue",0.1,25);
			A_AlertMonsters;
			A_SetPitch(pitch-3.0*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.97); }
		}

		FUNZ B 1 Bright {
			A_SetPitch(pitch-2.0*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.925); }
		}
		FUNZ B 1 {
			A_SetPitch(pitch-1.0*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.9); }
		}
		FUNZ B 1 {
			A_SetPitch(pitch-0.25*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.91); }
		}
		FUNZ B 1 A_SetPitch(pitch+0.35*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE)
		FUNZ B 1 {
			A_SetPitch(pitch-0.15*GetCvar("RO_WeaponRecoil"),SPF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.925); }
		}
		FUNZ B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.95); }
			A_SetBlend("Green",0.025,3);
		}
		FUNZ B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.96); }
			A_SetBlend("Green",0.025,3);
		}
		FUNZ B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.97); }
			A_SetBlend("Green",0.025,3);
		}
		FUNZ B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.98); }
			A_SetBlend("Green",0.025,3);
		}
		FUNZ B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(2.99); }
			A_SetBlend("Green",0.025,3);
		}
		FUNZ B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(3.0); }
			A_SetBlend("Green",0.025,3);
		}
		Goto Ready
	}
}