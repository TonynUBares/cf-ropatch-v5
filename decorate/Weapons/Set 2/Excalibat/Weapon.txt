/*
CREDITS:

Sprites:
Weapon - Apogee (ROTT), Zrrion the Insect (Big thanks for that donation!)
Projectile - ID (Doom 3)
Giant Rolling Stone, Stone fragments - Might and Magic (3DO), sourced from CutmanMike's Stone Golem mod.
Projectile - Zrrion the Insect, Captain J
Projectile Effects - Doom 3 (ID Software), sourced from Doomero's Doom 3 to Doom 2 mod.

Sounds:
Up - Valve (Team Fortress 2)
Pickup idle - ?
Pickup - Blizzard (Diablo 2)
Woosh - Perkristian
Fire, Idle, Growl - Blizzard (Diablo 2)
Charge - ?
Excalistorm - Totally no idea, it's a huge mashup of sounds made by me aeons ago.
Rolling stone humming - Raven (Wolfenstein 3)
Rolling stone, impact - Croteam (Serious Sam 3)
Switch to Excalistorm, Summon Rolling Stone - Blizzard (Diablo 2)
Switch to Rolling Stone - Raven (Wolfenstein 3)
Launch Rolling Stone - Raven (Wolfenstein 3)
*/

Actor ExcaliCharge : Inventory { Inventory.MaxAmount 26 }
ACTOR ExcaliMode : Inventory { Inventory.MaxAmount 1 }

ACTOR ExcaliAmmo : Ammo
{
	Inventory.Amount 1
	Inventory.MaxAmount 100
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 100
	Inventory.Icon "TNT1A0"
}

ACTOR ExcalibatStrachoNaLachoImbaInator : PowerupGiver
{
	Inventory.MaxAmount 0
	Powerup.Type "Baseball"
	Powerup.Color "Green" 0.0
	Inventory.PickupMessage "Teh monsters fears your baseball bat!" //Teh monsters are certainly afraid of your grammar.
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	States
	{
	Spawn:
		TNT1 A 1 Bright
		Loop
	}
}

ACTOR PowerBaseball : PowerFrightener
{
	Powerup.Duration 0x7FFFFFFF
}

ACTOR ExcalibatGiver : CustomInventory
{
	Inventory.PickupSound "Excalibat/Pick"
   	Inventory.PickupMessage "You've got the Excalibat! No comment needed, name says everything. (1)"
	DropItem "Excalibat"
	Scale 0.5
	+FLOAT
	+FLOATBOB
	States
	{
	Spawn:
		XBAT X 0 A_PlaySound("Excalibat/Chant",5,0.5,1)
		XBAT X 1 Bright A_SpawnItemEx("ExcalibatParticle",
		frandom(-3.0,3.0),frandom(-3.0,3.0),frandom(10.0,50.0),
		0,0,frandom(0.5,0.75),
		0,SXF_CLIENTSIDE,0)
		Loop
  Pickup:
	TNT1 A 0 A_JumpIfInventory("Excalibat",1,"JustAmmo")
  All:
	TNT1 A 0 A_GiveInventory("Excalibat",1)
	Stop
  JustAmmo:
	TNT1 A 0 A_GiveInventory("ExcaliAmmo",100)
  Remove:
    TNT1 A 0
    Stop
	}
}

ACTOR ExcalibatPickup : ExcalibatGiver {}

ACTOR Excalibat : Weapon 24515
{
	//$Category Weapons
	//$Title Excalibat
	//$Sprite BBTGX0
	Inventory.PickupSound "Excalibat/Pick"
   	Inventory.PickupMessage "You've got the Excalibat! Time to play some baseball with some demons :D"
	Obituary "%k showed his skills as an ace of the round table, evaporating %o off the Camelot park."
	//Weapon.UpSound "Excalibat/Bonk"
	Weapon.SelectionOrder 7
	Weapon.SlotNumber 1
	Weapon.SlotPriority 26.0
	Weapon.AmmoType "ExcaliAmmo"
	Weapon.AmmoGive 100
	Weapon.AmmoUse 20
	Weapon.AmmoUse2 1
	Weapon.Kickback 0
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobSpeed 1.6
	Weapon.BobRangeY 0.8
	Weapon.BobRangeX 1.0
	Scale 0.5
	+WEAPON.MELEEWEAPON
	+NOALERT
	+FLOATBOB
	+AMMO_OPTIONAL
	+ALT_AMMO_OPTIONAL
	+NOAUTOFIRE
	States
	{
	Spawn:
	SpawnLoop:
		XBAT X 1 Bright {
			if(GetCvar("RO_ClassRestriction")) {
			A_SpawnItemEx("ExcalibatParticleClass",
				frandom(-3.0,3.0),frandom(-3.0,3.0),frandom(10.0,50.0),
				0,0,frandom(0.5,0.75),0,SXF_CLIENTSIDE,0);
			} else {
				A_SpawnItemEx("ExcalibatParticle",
				frandom(-3.0,3.0),frandom(-3.0,3.0),frandom(10.0,50.0),
				0,0,frandom(0.5,0.75),0,SXF_CLIENTSIDE,0);
			}
		}
		Loop
	Nope:
		XBAT IJ 4
	NopeLoop:
		XBAT K 4
		XBAT K 0 A_JumpIf(CountInv("ExcaliAmmo") >= 20, "Ready")
		XBAT K 0 A_ReFire("NopeLoop")
	Ready:
		XBAT J 0 A_PlaySound("Excalibat/Idle",5)
		XBAT JI 4 A_WeaponReady(WRF_ALLOWRELOAD)
	ReadyMain:
		XBAT ABCDEDCB 2 A_WeaponReady(WRF_ALLOWRELOAD)
		XBAT A 0 A_Jump(12,1)
		Loop
		XBAT A 0 A_PlaySound("Excalibat/Idle",5)
		XBAT A 0 A_Jump(256,"Eyeroll1","Eyeroll2","Eyeroll3")
	Eyeroll1:
		XBAT EFGHE 4 A_WeaponReady(WRF_ALLOWRELOAD)
		Goto ReadyMain+6
	Eyeroll2:
		XBAT EHGFE 4 A_WeaponReady(WRF_ALLOWRELOAD)
		Goto ReadyMain+6
	Eyeroll3:
		XBAT IJKKKJI 4 A_WeaponReady(WRF_ALLOWRELOAD)
		Goto ReadyMain+6
	Deselect:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,1,1,0)
	DeselectLoop:
		XBAT I 0 {
			A_TakeInventory("ExcaliCharge",99);
			A_ZoomFactor(1.0);
			SetPlayerProperty(0,0,0);
			A_StopSound(5);
		}
		XBAT IIIIJJJJ 1 A_Lower(12)
		XBAT K 1 A_Lower(18)
		Wait
	Select:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,0,1,0)
	SelectLoop:
		XBAT K 1 A_Raise(18)
		Loop
	Fire:
    	XBAT A 0 A_JumpIfInventory("ExcaliAmmo",20,1)
    	Goto Nope
		XBAT A 1 A_WeaponOffset(3.0, 2.0, WOF_ADD|WOF_INTERPOLATE)
		XBAT B 1 A_WeaponOffset(5.0, 3.0, WOF_ADD|WOF_INTERPOLATE)
		XBAT C 1 A_WeaponOffset(10.0, 10.0, WOF_ADD|WOF_INTERPOLATE)
		XBAT D 1 A_WeaponOffset(19.0, 17.0, WOF_ADD|WOF_INTERPOLATE)
		XBAT E 1 A_WeaponOffset(17.0, 22.0, WOF_ADD|WOF_INTERPOLATE)
	FireMain:
		XBAT E 0 {
			A_WeaponOffset(0.0, 32.0); //Reset the offsets.
			A_SetBlend("Green", 0.3, 25);
			if(GetCvar("RO_QuakeRecoil")) { A_QuakeEx(4,4,2,11,0,2,"none",QF_SCALEDOWN); }
			A_PlaySound("Excalibat/Woosh",1);
			A_PlaySound("Excalibat/Growl",5);
			A_PlaySound("Excalibat/Fire",6);
		}
		XBAT L 1 Bright {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.7); }
			A_SetAngle(Angle + 1.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_AlertMonsters;
			A_FireCustomMissile("ExcalibatBolt",-40,1,0,0);
			A_FireCustomMissile("ExcalibatBolt",-35,0,0,0);
		}
		XBAT M 1 Bright {
			A_WeaponOffset(15.0, 0.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.75); }
			A_SetAngle(Angle + 3.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_AlertMonsters;
			A_FireCustomMissile("ExcalibatBolt",-30,0,0,0);
			A_FireCustomMissile("ExcalibatBolt",-25,0,0,0);
		}
		XBAT M 1 Bright {
			A_WeaponOffset(-15.0, 0.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.8); }
			A_SetAngle(Angle + 5.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_AlertMonsters;
			A_FireCustomMissile("ExcalibatBolt",-20,0,0,0);
			A_FireCustomMissile("ExcalibatBolt",-15,0,0,0);
		}
		XBAT N 1 Bright {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.85); }
			A_SetAngle(Angle + 7.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_AlertMonsters;
			A_FireCustomMissile("ExcalibatBolt",-10,0,0,0);
			A_FireCustomMissile("ExcalibatBolt",-5,0,0,0);
		}
		XBAT N 1 Bright {
			A_WeaponOffset(25.0, 12.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.875); }
			A_SetAngle(Angle + 7.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_AlertMonsters;
			A_FireCustomMissile("ExcalibatBolt",5,0,0,0);
			A_FireCustomMissile("ExcalibatBolt",10,0,0,0);
		}
		XBAT N 1 Bright {
			A_WeaponOffset(25.0, 12.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.9); }
			A_SetAngle(Angle + 5.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_AlertMonsters;
			A_FireCustomMissile("ExcalibatBolt",15,0,0,0);
			A_FireCustomMissile("ExcalibatBolt",20,0,0,0);
		}
		XBAT O 1 Bright {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.925); }
			A_SetAngle(Angle + 3.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_AlertMonsters;
			A_FireCustomMissile("ExcalibatBolt",25,0,0,0);
			A_FireCustomMissile("ExcalibatBolt",30,0,0,0);
		}
		XBAT O 1 Bright {
			A_WeaponOffset(-25.0, 12.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.95); }
			A_SetAngle(Angle + 1.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_AlertMonsters;
			A_FireCustomMissile("ExcalibatBolt",35,0,0,0);
			A_FireCustomMissile("ExcalibatBolt",40,0,0,0);
		}
		XBAT O 1 Bright {
			A_WeaponOffset(-25.0, 12.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.975); }
		}
		XBAT O 1 Bright {
			A_WeaponOffset(-25.0, 12.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.985); }
		}
      	TNT1 A 18 A_ZoomFactor(1.0)
		XBAT E 1 A_WeaponOffset(36.0, 92.0)
		XBAT D 1 A_WeaponOffset(22.0, 75.0, WOF_INTERPOLATE)
		XBAT C 1 A_WeaponOffset(15.0, 44.0, WOF_INTERPOLATE)
		XBAT B 1 A_WeaponOffset(7.0, 36.0, WOF_INTERPOLATE)
		XBAT A 1 A_WeaponOffset(0.0, 32.0, WOF_INTERPOLATE)
		XBAT A 0 A_ReFire
		Goto ReadyMain
	AltFire:
       	XBAT A 0 A_JumpIfInventory("ExcaliMode",1,"StoneFire")
       	XBAT A 0 A_JumpIfInventory("ExcaliAmmo",1,1)
       	Goto Nope
		XBAT A 0 {
			A_PlaySound("Excalibat/Charge",5,1);
			//A_PlaySound("Excalibat/Growl",6);
			A_GiveInventory("ExcalibatStrachoNaLachoImbaInator",1);
			A_AlertMonsters;
		}
		XBAT A 0 { A_GiveInventory("ExcaliCharge",16); if(GetCvar("RO_QuakeRecoil")) { A_QuakeEx(4,4,4,52,0,2,"none",QF_SCALEUP); } } //26 charge points for old rev-up time.
	AltDown:
		XBAT A 1 A_WeaponOffset(3.0, 2.0, WOF_ADD|WOF_INTERPOLATE)
		XBAT B 1 A_WeaponOffset(5.0, 3.0, WOF_ADD|WOF_INTERPOLATE)
		XBAT C 1 A_WeaponOffset(10.0, 10.0, WOF_ADD|WOF_INTERPOLATE)
		XBAT D 1 A_WeaponOffset(19.0, 17.0, WOF_ADD|WOF_INTERPOLATE)
		XBAT E 1 A_WeaponOffset(17.0, 22.0, WOF_ADD|WOF_INTERPOLATE)
		XBAT EFGH 2
		{
			A_WeaponOffset(8.0 + frandom(6.0,-6.0), 19.0 + frandom(6.0, -6.0), WOF_ADD|WOF_INTERPOLATE);
			A_TakeInventory("ExcaliCharge",1);
			Return A_JumpIf(CountInv("ExcaliCharge") <= 0, "AltStart");
		}
		Loop
	AltStart:
		XBAT N 0 {
			A_WeaponOffset(300.0, 0.0);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.7); }
			A_PlaySound("Excalibat/Loop",5,1,1,ATTN_NONE);
			SetPlayerProperty(0,1,0);
			A_ChangeFlag("INVULNERABLE",1);
			A_ChangeFlag("REFLECTIVE",1);
			A_GiveInventory("ExcaliCharge",10);
		}
	AltMain:
		XBAT N 1 {
		 	A_WeaponOffset(-75.0 + frandom(0.0,15.0), 0.0, WOF_ADD|WOF_INTERPOLATE);
			A_ChangeVelocity(frandom(6.0,-6.0), frandom(6.0,-6.0), 0, CVF_RELATIVE);
			A_SetBlend("Green", 0.25, 1);
			A_SetAngle(Angle + 12.0 + frandom(0.5,2.0), SPF_INTERPOLATE);
			A_FireCustomMissile("ExcalibatSwirl",0,1,0,0);
			A_TakeInventory("ExcaliAmmo",1,TIF_NOTAKEINFINITE);
			
			A_FireCustomMissile("ExcalibatChaosBolt",0,0,0,0,0);
			//A_FireCustomMissile("ExcalibatChaosBolt",45,0,0,0,0);
			A_FireCustomMissile("ExcalibatChaosBolt",90,0,0,0,0);
			//A_FireCustomMissile("ExcalibatChaosBolt",135,0,0,0,0);
			A_FireCustomMissile("ExcalibatChaosBolt",180,0,0,0,0);
			//A_FireCustomMissile("ExcalibatChaosBolt",225,0,0,0,0);
			A_FireCustomMissile("ExcalibatChaosBolt",270,0,0,0,0);
			//A_FireCustomMissile("ExcalibatChaosBolt",315,0,0,0,0);
			
			A_TakeInventory("ExcaliCharge",1);
		}
		XBAT N 0 A_JumpIf(CountInv("ExcaliAmmo") <= 0,"EndTheMadness")
		XBAT N 0 A_JumpIfInventory("ExcaliCharge", 1, "AltMain")
		XBAT N 0 A_ReFire("AltRestart")
		Goto EndTheMadness
	AltRestart:
		XBAT N 0 {
			A_WeaponOffset(300.0, 0.0);
			A_GiveInventory("ExcaliCharge",10);
		}
		Goto AltMain
	EndTheMadness:
		TNT1 A 0 {
			A_TakeInventory("ExcaliCharge",99);
			SetPlayerProperty(0,0,0);
			A_PlaySound("Excalibat/End",5,1,0,ATTN_NORM);
			A_TakeInventory("PowerBaseball",1);
			A_SetBlend("Green", 0.3, 25);
		}
		
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.75); } A_SetAngle(Angle + 4.0, SPF_INTERPOLATE); }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.775); } A_SetAngle(Angle + 3.0, SPF_INTERPOLATE); }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.8); } A_SetAngle(Angle + 3.5, SPF_INTERPOLATE); }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.825); } A_SetAngle(Angle + 3.0, SPF_INTERPOLATE); }
   	 	TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.85); } A_SetAngle(Angle + 2.0, SPF_INTERPOLATE); }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.875); } A_SetAngle(Angle + 1.0, SPF_INTERPOLATE); }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.9); } A_SetAngle(Angle + 0.75, SPF_INTERPOLATE); }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.925); } A_SetAngle(Angle + 0.5, SPF_INTERPOLATE); }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.95); } A_SetAngle(Angle + 0.25, SPF_INTERPOLATE); }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.975); } A_SetAngle(Angle + 0.1, SPF_INTERPOLATE); }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.985); } A_ChangeFlag("INVULNERABLE",0); A_ChangeFlag("REFLECTIVE",0); A_WeaponOffset(36.0, 92.0, WOF_INTERPOLATE); }
		XBAT E 1 { A_WeaponOffset(36.0, 92.0, WOF_INTERPOLATE); if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.995); } }
		XBAT D 1 { A_WeaponOffset(22.0, 75.0, WOF_INTERPOLATE); if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(1.0); } }
		XBAT C 1 A_WeaponOffset(15.0, 44.0, WOF_INTERPOLATE)
		XBAT B 1 A_WeaponOffset(7.0, 36.0, WOF_INTERPOLATE)
		XBAT A 1 A_WeaponOffset(0.0, 32.0, WOF_INTERPOLATE)
    	Goto ReadyMain

	StoneFire:
		TNT1 A 0 A_JumpIfInventory("ExcaliAmmo",100,1)
		Goto Nope
		XBAT A 1 A_WeaponOffset(3.0, 2.0, WOF_ADD|WOF_INTERPOLATE)
		XBAT B 1 A_WeaponOffset(5.0, 3.0, WOF_ADD|WOF_INTERPOLATE)
		XBAT C 1 A_WeaponOffset(10.0, 10.0, WOF_ADD|WOF_INTERPOLATE)
		XBAT D 1 A_WeaponOffset(19.0, 17.0, WOF_ADD|WOF_INTERPOLATE)
		XBAT E 1 A_WeaponOffset(17.0, 22.0, WOF_ADD|WOF_INTERPOLATE)
		TNT1 A 11 {
			A_WeaponOffset(0.0, 72.0, WOF_INTERPOLATE);
			SetPlayerProperty(0,1,0);
			A_PlaySound("Excalistone/Spawn",5);
			A_TakeInventory("ExcaliAmmo",100,TIF_NOTAKEINFINITE);
			A_SpawnItemEx("StoneReadyForHurrr",60,0,0,0,0,0,0,SXF_ORIGINATOR|SXF_SETMASTER);
			if(GetCvar("RO_QuakeRecoil")) { A_QuakeEx(3,3,5,23,0,2,"none",QF_SCALEUP); }
			A_Overlay(-2,"LeftHand");
			A_Overlay(2,"RightHand");
		}
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.9); } }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.7); } }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.65); } }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.675); } }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.7); } }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.8); } }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.9); } }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.925); } }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.95); } }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.975); } }
		TNT1 A 1 { if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(1.0); } }
		TNT1 A 9
		XBAT E 0 {
			A_WeaponOffset(0.0, 32.0); //Reset the offsets.
			A_SetBlend("Green", 0.4, 35);
			SetPlayerProperty(0,0,0);
			if(GetCvar("RO_QuakeRecoil")) { A_QuakeEx(6,6,6,25,0,2,"none",QF_SCALEDOWN); }
			A_PlaySound("Excalistone/Launch",1,1.0,0,ATTN_NONE);
			A_PlaySound("Excalibat/Growl",5);
			A_AlertMonsters;
			A_FireCustomMissile("RollingStone");
		}
		XBAT L 1 Bright {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.7); }
			A_SetAngle(Angle + 1.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		XBAT M 1 Bright {
			A_WeaponOffset(15.0, 0.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.75); }
			A_SetAngle(Angle + 3.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		XBAT M 1 Bright {
			A_WeaponOffset(-15.0, 0.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.8); }
			A_SetAngle(Angle + 5.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		XBAT N 1 Bright {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.85); }
			A_SetAngle(Angle + 7.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		XBAT N 1 Bright {
			A_WeaponOffset(25.0, 12.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.875); }
			A_SetAngle(Angle + 7.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		XBAT N 1 Bright {
			A_WeaponOffset(25.0, 12.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.9); }
			A_SetAngle(Angle + 5.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		XBAT O 1 Bright {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.925); }
			A_SetAngle(Angle + 3.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		XBAT O 1 Bright {
			A_WeaponOffset(-25.0, 12.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.95); }
			A_SetAngle(Angle + 1.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		XBAT O 1 Bright {
			A_WeaponOffset(-25.0, 12.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.975); }
		}
		XBAT O 1 Bright {
			A_WeaponOffset(-25.0, 12.0, WOF_ADD|WOF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.985); }
		}
      	TNT1 A 18 A_ZoomFactor(1.0)
		XBAT E 1 A_WeaponOffset(36.0, 92.0)
		XBAT D 1 A_WeaponOffset(22.0, 75.0, WOF_INTERPOLATE)
		XBAT C 1 A_WeaponOffset(15.0, 44.0, WOF_INTERPOLATE)
		XBAT B 1 A_WeaponOffset(7.0, 36.0, WOF_INTERPOLATE)
		XBAT A 1 A_WeaponOffset(0.0, 32.0, WOF_INTERPOLATE)
		XBAT A 0 A_ReFire
		Goto ReadyMain
	RightHand:
		HEMD A 1 A_OverlayOffset(2, 1.0, -2.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 2.0, -3.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 3.0, -4.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 3.0, -5.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 4.0, -7.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 4.0, -9.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 3.0, -7.0, WOF_ADD|WOF_INTERPOLATE)
		
		HEMD A 1 A_OverlayOffset(2, 2.0, 2.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 4.0, 2.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 5.0, 3.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 7.0, 7.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 3.0, 3.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 4.0, 4.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 6.0, 5.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 7.0, 6.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 8.0, 7.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 11.0, 8.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 13.0, 8.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 15.0, 8.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD A 1 A_OverlayOffset(2, 17.0, 9.0, WOF_ADD|WOF_INTERPOLATE)
		TNT1 A 1 A_OverlayOffset(2, 0.0, 32.0)
		Stop
	LeftHand:
		HEMD B 1 A_OverlayOffset(-2, -1.0, -2.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -2.0, -3.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -3.0, -4.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -3.0, -5.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -4.0, -7.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -4.0, -9.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -3.0, -7.0, WOF_ADD|WOF_INTERPOLATE)
		
		HEMD B 1 A_OverlayOffset(-2, -2.0, 2.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -4.0, 2.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -5.0, 3.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -7.0, 7.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -3.0, 3.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -4.0, 4.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -6.0, 5.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -7.0, 6.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -8.0, 7.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -11.0, 8.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -13.0, 8.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -15.0, 8.0, WOF_ADD|WOF_INTERPOLATE)
		HEMD B 1 A_OverlayOffset(-2, -17.0, 9.0, WOF_ADD|WOF_INTERPOLATE)
		TNT1 A 1 A_OverlayOffset(-2, 0.0, 32.0)
		Stop
		
	Reload: //Switches to Rolling Stone Mode
		TNT1 A 0 A_JumpIfInventory("ExcaliMode",1,"UnReload")
		TNT1 A 0 {
			A_PlaySound("Excalibat/RockNRoll",5);
			A_PlaySound("Excalistone/Chunk",1);
			//A_PlaySound("Excalibat/Growl",6);
			A_SetBlend("Grey", 0.25, 24);
			if(GetCvar("RO_QuakeRecoil")) { A_QuakeEx(3,3,3,16,0,5,"none",QF_SCALEDOWN); }
			A_Print("\c[DarkGray]''Sisyphus Best Buddy'' Mode.\n\c[white]Click altfire on full charge to unleash a rolling stone.");
			A_GiveInventory("ExcaliMode",1);
		}
		TNT1 AA 0 A_SpawnItemEx("MissileExpSmoke",
		frandom(15.0,-15.0), frandom(15.0,-15.0), 30+frandom(15.0,-15.0),
		frandom(0.1,0.2),frandom(0.1,0.2),frandom(0.1,0.2),0,SXF_CLIENTSIDE,0)
		XBAT HHHHGGGGFFFFEEEEHHHHGGGGFFFF 1 A_WeaponOffset(0.0 + frandom(3.0, -3.0), 32.0 + frandom(3.0, -3.0), WOF_INTERPOLATE) //A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    	XBAT E 4 A_WeaponOffset(0.0,32.0)
		Goto ReadyMain
	UnReload: //Switches to Excalistorm Mode
		TNT1 A 0 {
			A_PlaySound("Excalibat/Excalistorm",5);
			//A_PlaySound("Excalibat/Growl",6);
			A_SetBlend("Green", 0.25, 24);
			A_QuakeEx(3,3,3,16,0,5,"none",QF_SCALEDOWN);
			A_Print("\c[Green]Excalistorm Mode.\n\c[white]Hold altfire to SPEEEEEN.");
			A_TakeInventory("ExcaliMode",1);
		}
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("ExcalibatParticle",
		frandom(15.0,-15.0), frandom(15.0,-15.0), 30+frandom(15.0,-15.0),
		0,0,frandom(0.5,0.75),0,SXF_CLIENTSIDE,0)
		XBAT HHHHGGGGFFFFEEEEHHHHGGGGFFFF 1 A_WeaponOffset(0.0 + frandom(3.0, -3.0), 32.0 + frandom(3.0, -3.0), WOF_INTERPOLATE) //A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
    	XBAT E 4 A_WeaponOffset(0.0,32.0)
		Goto ReadyMain
	}
}