/*
CRUDUTS:

GRAPHIC WORK:
Gun sprite - ZrrionTheInsect, TurtleRock (edit of weapon from Evolve)
Pillowblaster - animated the thing, made muzzleflash and pickup.

SOUND WORK:
Fire, add, up, reload, beep - Tripwire (Killing Floor, thanks to Marrub for rips~)
Shield deploy, healshot - Splash Damage/ID Software (Enemy Territory: Quake Wars)
Dry - Raven Software (Wolf2k9)

*/

Actor AntonovShieldUp : Inventory { Inventory.MaxAmount 1 }
Actor AntonovHealDelay : AntonovShieldUp {}
Actor AntonovInfo : AntonovShieldUp {}

actor AntonovGiver : CustomInventory
{
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You've got the ''Antonov'' splitter rifle! I know what you are thinking....please dont camp with the shield,its boring,go nuts instead"
	Obituary "%o was swarmed all over by %k's ''Antonov."
	States
	{
	Spawn:
		EVOG X 1 
		Loop
  Pickup:
	TNT1 A 0 A_JumpIfInventory("Antonov",1,"JustAmmo")
  All:
	TNT1 A 0 A_GiveInventory("Antonov",1)
	Stop
  JustAmmo:
	TNT1 A 0 A_GiveInventory("Boolet",40)
	TNT1 A 0 A_GiveInventory("PlasmaAmmo",10)
  Remove:
    TNT1 A 0
    Stop
	}
}

Actor Antonov : Weapon 24518
{
	//$Category Weapons
	//$Title Antonov
	//$Sprite EVOGX0
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You've got the ''Antonov'' splitter rifle! I know what you are thinking....please dont camp with the shield,its boring,go nuts instead"
	Obituary "%o was swarmed all over by %k's ''Antonov."
	Weapon.UpSound "Antonov/Up"
	Weapon.SelectionOrder 5
	Weapon.AmmoType1 "Boolet"
	Weapon.AmmoType2 "PlasmaAmmo"
	Weapon.AmmoGive1 40
	Weapon.AmmoGive2 10
	Weapon.AmmoUse1 5
	Weapon.AmmoUse2 0 //10
	Weapon.SlotNumber 4
	Weapon.SlotPriority 26.0
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobSpeed 1.4
	Weapon.BobRangeY 0.6
	Weapon.BobRangeX 1.2
	+NOALERT
	+NOAUTOAIM
	+ALT_AMMO_OPTIONAL
	states
	{
	Spawn:
		EVOG X -1
		Stop
	Ready:
		EVOG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Loop
	DeadLowered:
		TNT1 A -1
		Stop
	Select:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,10,1,0)
		EVOG A 0 {
			if(CountInv("AntonovShieldUp"))
			{
				A_Overlay(2,"Honkytonk"); 
				/*A_OverlayFlags(2,PSPF_ALPHA|PSPF_RENDERSTYLE,1);
				A_OverlayStyle(2,STYLE_Add);*/
			}
		}
	SelectLoop:
		EVOG A 1 A_Raise(12)
		Loop
	Deselect:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,11,1,0)
	DeselectLoop:
		EVOG A 1 A_Lower(12)
		Loop
	Fire:
		EVOF A 1 Bright {
			A_PlaySound("Antonov/Fire",1,0.8);
			A_PlaySound("Antonov/Add",5,0.8);
			A_Gunflash;
			A_AlertMonsters;
			A_GiveInventory("BooletCasing",5);
			if(CountInv("CaliberUpgrade"))
			{
				A_PlaySound("Weapons/WolgirHEndFire",0);
				A_FireCustomMissile("CaliberAntonovSlug",0,1,10);
			}
			else
			{
				A_FireCustomMissile("AntonovSlug",0,1,10);
			}
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.98); }
			A_WeaponOffset(5.0,5.0,WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(Pitch - 0.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 0.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		EVOF A 1 Bright {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.96); }
			A_WeaponOffset(7.0,7.0,WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(Pitch - 1.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 1.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		EVOG B 1 {
			A_PlaySound("Antonov/Reload",6);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.95); }
			A_WeaponOffset(3.0,3.0,WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(Pitch - 0.75*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 0.75*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
	Rest:
		EVOG B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.96); }
			A_WeaponOffset(1.0,1.0,WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(Pitch - 0.25*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 0.25*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		EVOG B 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.97); }
			A_SetPitch(Pitch - 0.1*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 0.1*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		EVOG C 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.98); }
			A_WeaponOffset(-1.0,-1.0,WOF_INTERPOLATE|WOF_ADD);
		}
		EVOG C 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.99); }
			A_WeaponOffset(-3.0,-3.0,WOF_INTERPOLATE|WOF_ADD);
		}
		EVOG C 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.995); }
			A_WeaponOffset(-4.0,-4.0,WOF_INTERPOLATE|WOF_ADD);
		}
		EVOG D 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(1.0); }
			A_WeaponOffset(-3.0,-3.0,WOF_INTERPOLATE|WOF_ADD);
		}
		EVOG D 1 A_WeaponOffset(-3.0,-3.0,WOF_INTERPOLATE|WOF_ADD)
		EVOG D 1 A_WeaponOffset(-2.0,-2.0,WOF_INTERPOLATE|WOF_ADD)
		EVOG D 1 A_WeaponOffset(-1.0,-1.0,WOF_INTERPOLATE|WOF_ADD)
		EVOG A 1 A_JumpIf(CountInv("AntonovHealDelay"),1)
		Goto Ready
		EVOG A 12 A_TakeInventory("AntonovHealDelay",1)
		Goto Ready
	AltFire:
		EVOG A 0 A_JumpIf(CountInv("AntonovShieldUp"), "Detonate")
		EVOG A 0 A_JumpIf(CountInv("PlasmaAmmo") >= 10, 1)
		Goto Dry
		EVOF B 1 Bright {
			if(!CountInv("AntonovInfo") && !GetCvar("RO_IGotIt")) {
				A_Print("\c[white]Undeploy your shield wall using \c[cyan]secondary fire\c[white] again as it uses your ammo as it goes.");
				A_GiveInventory("SteamInfo",1);
			}
			A_Gunflash;
			A_AlertMonsters;
			A_Overlay(2,"Honkytonk");
			/*A_OverlayFlags(2,PSPF_ALPHA|PSPF_RENDERSTYLE,1);
			A_OverlayStyle(2,STYLE_Add);*/
			A_PlaySound("Antonov/Deploy",1);
			A_GiveInventory("AntonovShieldUp",1);
			A_FireCustomMissile("AntonovShieldProjectile",0,1,10);

			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.98); }
			A_WeaponOffset(5.0,5.0,WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(Pitch - 0.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 0.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		EVOF B 1 Bright {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.96); }
			A_WeaponOffset(7.0,7.0,WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(Pitch - 1.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 1.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		EVOG B 1 {
			A_PlaySound("Antonov/Reload",6);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.95); }
			A_WeaponOffset(3.0,3.0,WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(Pitch - 0.75*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 0.75*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		Goto Rest
	Reload:
		EVOG A 0 A_JumpIf(CountInv("BooletCasing") >= 40, 1)
		Goto Dry
		EVOF C 1 Bright {
			A_Gunflash;
			A_AlertMonsters;
			A_GiveInventory("AntonovHealDelay",1);
			A_TakeInventory("BooletCasing",40,TIF_NOTAKEINFINITE);
			/*A_OverlayFlags(2,PSPF_ALPHA|PSPF_RENDERSTYLE,1);
			A_OverlayStyle(2,STYLE_Add);*/
			A_PlaySound("Antonov/Fire",1,0.8); //A_PlaySound("Antonov/Deploy",1);
			A_PlaySound("Antonov/Healshot",5);
			A_FireCustomMissile("AntonovHealthProjectile",0,0,10);

			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.98); }
			A_WeaponOffset(5.0,5.0,WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(Pitch - 0.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 0.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		EVOF C 1 Bright {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.96); }
			A_WeaponOffset(7.0,7.0,WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(Pitch - 1.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 1.5*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		EVOG B 1 {
			A_PlaySound("Antonov/Reload",6);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.95); }
			A_WeaponOffset(3.0,3.0,WOF_INTERPOLATE|WOF_ADD);
			A_SetPitch(Pitch - 0.75*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetAngle(Angle - 0.75*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
		}
		Goto Rest
	Detonate:
		EVOG A 0 {
			A_PlaySound("Antonov/Beep",5);
			A_TakeInventory("AntonovShieldUp",1);
			A_Overlay(2,"");
		}
		EVOG AAA 1 A_WeaponOffset(1.0,0.5,WOF_INTERPOLATE|WOF_ADD)
		EVOG AAA 1 A_WeaponOffset(-1.0,-0.5,WOF_INTERPOLATE|WOF_ADD)
		EVOG AAA 1 A_WeaponOffset(1.0,0.5,WOF_INTERPOLATE|WOF_ADD)
		EVOG AAA 1 A_WeaponOffset(-1.0,-0.5,WOF_INTERPOLATE|WOF_ADD)
		EVOG A 15
		Goto Ready
	Dry:
		EVOG A 0 {
			A_PlaySound("Antonov/Dry",5);
		}
		EVOG AAA 1 A_WeaponOffset(1.0,0.5,WOF_INTERPOLATE|WOF_ADD)
		EVOG AAA 1 A_WeaponOffset(-1.0,-0.5,WOF_INTERPOLATE|WOF_ADD)
		EVOG A 15
		Goto Ready
	Honkytonk:
		EVOG Z 1 /*A_OverlayAlpha(2, 0.9)
		EVOG Z 1 A_OverlayAlpha(2, 0.8)
		EVOG Z 1 A_OverlayAlpha(2, 0.7)
		EVOG Z 1 A_OverlayAlpha(2, 0.8)
		EVOG Z 1 A_OverlayAlpha(2, 0.9)
		EVOG Z 1 A_OverlayAlpha(2, 1.0)*/
		Loop
	Gunflash:
		TNT1 A 4 A_Light2
		TNT1 A 4 A_Light1
		TNT1 A 0 A_Light0
		Stop
	}
}