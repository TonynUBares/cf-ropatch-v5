/*
CREDITS:
Sprites - LucasArts (Star Wars: Dark Forces)
Pickup - Made by me from mashup of Scuba Steve's bits (Action Doom)

Fire - Running with Scissors (Postal)
Add Fire - ?
Fire2, Shell Launch, Nope, Bounce - Epic Games, People can Fly (Bulletstorm)
Music - Tchaikovsky, Overture 1812
*/

actor MegaShellOut : Inventory { Inventory.MaxAmount 1 }

actor PantokratorGiver : CustomInventory
{
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Omfg'ed by the sight of ''Pantokrator'' cannon shotgun! THIS GUN IS PAN-TASTIC.....why i made this class of jokes?"
	Obituary "%o couldn't deny %k's worldview. Destructionism goes pretty well with ''Pantokrator''."
	States
	{
	Spawn:
		PANT X 1 
		Loop
  Pickup:
	TNT1 A 0 A_JumpIfInventory("Pantokrator",1,"JustAmmo")
  All:
	TNT1 A 0 A_GiveInventory("Pantokrator",1)
	Stop
  JustAmmo:
	TNT1 A 0 A_GiveInventory("ErasusAmmo",30)
  Remove:
    TNT1 A 0
    Stop
	}
}

ACTOR Pantokrator : DoomWeapon 24517
{
	//$Category Weapons
	//$Title Pantokrator
	//$Sprite PANTX0
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Omfg'ed by the sight of ''Pantokrator'' cannon shotgun! THIS GUN IS PAN-TASTIC.....why i made this class of jokes?"
	Obituary "%o couldn't deny %k's worldview. Destructionism goes pretty well with ''Pantokrator''."
	Weapon.UpSound "Pantokrator/Up"
	Weapon.SelectionOrder 6
	Weapon.AmmoType "ErasusAmmo"
	Weapon.AmmoGive 30
	Weapon.AmmoUse 10
	Weapon.Kickback 55
	Weapon.SlotNumber 3
	Weapon.SlotPriority 26.0
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobSpeed 1.4
	Weapon.BobRangeY 0.6
	Weapon.BobRangeX 1.2
	Scale 1.1
	+NOAUTOFIRE
	+NOALERT
	States
	{
	Spawn:
		PANT X -1
		Stop
	Ready:
		PANT A 1 A_WeaponReady
		Loop
	Deselect:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,1,1,0)
	DeselectLoop:
		PANT A 0 A_ZoomFactor(1.0)
		PANT A 1 A_Lower(12)
		Wait
	Select:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,0,1,0)
	SelectLoop:
		PANT A 1 A_Raise(12)
		Loop
	Fire:
		PANT B 0 Bright {
			A_GunFlash;
			A_AlertMonsters;
			A_Recoil(9.0);
			if(GetCvar("RO_QuakeRecoil")) {
				A_QuakeEx(3,3,3,35,0,128,"none",QF_SCALEDOWN|QF_WAVE,2.0,1.0,2.0);
				A_QuakeEx(3,3,3,35,0,128,"none",QF_SCALEDOWN);
			}
			A_GiveInventory("ErasusCasing",10);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.7); }
			If(CountInv("QuadUpgrade")) {
					A_SetBlend("Blue",0.175,18);
					A_PlaySound("Weapons/QuadShot",4);
					A_FireBullets(21, 4.25, 150, 36, "QuadPuff");
			}
			Else {
					A_FireBullets(21, 4.25, 150, 18, "ErasusPuff");
			}
			A_PlaySound("Pantokrator/AddFire",1,0.6);
			A_PlaySound("Pantokrator/Fire",5,0.9);
			A_PlaySound("Pantokrator/Fire2",6,0.7);
		}
	Gesundheit:
		PANT B 1 Bright {
			A_SetAngle(Angle - 4.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch - 4.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.8); }
			A_WeaponOffset(3.0,3.0,WOF_ADD|WOF_INTERPOLATE);
		}
		PANT B 1 Bright {
			A_SetAngle(Angle - 2.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch - 2.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.9); }
			A_WeaponOffset(7.0,7.0,WOF_ADD|WOF_INTERPOLATE);
		}
		PANT C 1 Bright {
			A_SetAngle(Angle - 1.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch - 1.0*GetCvar("RO_WeaponRecoil"), SPF_INTERPOLATE);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.925); }
			A_WeaponOffset(4.0,4.0,WOF_ADD|WOF_INTERPOLATE);
		}
		PANT C 1 Bright {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.95); }
			A_WeaponOffset(-1.0,-1.0,WOF_ADD|WOF_INTERPOLATE);
		}
		PANT D 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.975); }
			A_WeaponOffset(-2.0,-2.0,WOF_ADD|WOF_INTERPOLATE);
		}
		PANT D 1 {
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(1.0); }
			A_WeaponOffset(-3.0,-3.0,WOF_ADD|WOF_INTERPOLATE);
		}
		PANT D 1 A_WeaponOffset(-3.0,-3.0,WOF_ADD|WOF_INTERPOLATE)
		PANT AA 1 A_WeaponOffset(-2.0,-2.0,WOF_ADD|WOF_INTERPOLATE)
		PANT A 1 A_WeaponOffset(-1.0,-1.0,WOF_ADD|WOF_INTERPOLATE)
		PANT A 9 A_ReFire
		Goto Ready
	AltFire:
		PANT A 0 A_JumpIfInventory("ErasusCasing",200,1)
		Goto Nope
		PANT A 0 A_JumpIfInventory("MegaShellOut",1,"Nope")
		PANT A 0 {
			if(GetCvar("RO_QuakeRecoil")) {
				A_QuakeEx(6,6,6,35,0,128,"none",QF_SCALEDOWN|QF_WAVE,3.0,1.0,3.0);
				A_QuakeEx(6,6,6,35,0,128,"none",QF_SCALEDOWN);
			}
			A_GiveInventory("MegaShellOut",1);
			A_GunFlash;
			A_AlertMonsters;
			A_Recoil(18.0);
			if(GetCvar("RO_ZoomRecoil")) { A_ZoomFactor(0.7); }
			If(CountInv("QuadUpgrade")) {
				A_SetBlend("Blue",0.225,24);
				A_PlaySound("Weapons/QuadShot",6);
			}
			A_PlaySound("Pantokrator/AddFire",1,0.6);
			A_PlaySound("MegaShell/Fire",5,0.9);
			A_FireCustomMissile("MegaShell",0,0,10);
			A_TakeInventory("ErasusCasing",200);
		}
		Goto Gesundheit
	Nope:
		PANT A 20 { A_WeaponReady(WRF_NOSECONDARY); A_PlaySound("MegaShell/Nope",6); }
		Goto Ready
	Flash:
		TNT1 A 4 A_Light2
		TNT1 A 2 A_Light1
		TNT1 A 0 A_Light0
		Stop
   }
}