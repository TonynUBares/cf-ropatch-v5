actor StickyThrowPower : Ammo
{
   Inventory.Amount 1
   Inventory.MaxAmount 10
   Ammo.BackpackAmount 0
   Ammo.BackpackMaxAmount 10
   Inventory.Icon "TNT1A0"
   Inventory.PickupMessage "Picked up an UNLIMITED POWERRR!!!!"
   States
   {
   Spawn:
      TNT1 A -1
      Stop
   }
}

actor StickyBombItem : CustomInventory
{
	Tag "Sticky Bombs"
	Inventory.Amount 1
	Inventory.MaxAmount 1
	Inventory.PickupSound "items/jetthruster"
	Inventory.UseSound "used/jetthruster"
	Inventory.Icon "ARTISBMG"
	Scale 0.6
	+INVENTORY.UNDROPPABLE
	+INVBAR
	states
	{
	Spawn:
		SBMB A -1
		Stop
	Use:
		TNT1 A 0 A_JumpIf(!CountInv("StickyAmmo"),"Nope")
		TNT1 A 0 A_SelectWeapon("StickyBombWeapon")
		Fail
	Nope:
		TNT1 A 0 {
			A_PlaySound("intermission/tick",2);
			A_Print("\c[white]Sticky bombs ammo required.");
		}
		Fail
  }
}

actor StickyMode : Inventory { Inventory.MaxAmount 1 }

ACTOR StickyBombWeapon : Weapon 24542
{
	//$Category Weapons
	//$Title Sticky Bomb
	//$Sprite SBMBA0
	Tag "Sticky Bomb"
    Inventory.PickupSound "misc/i_pkup"
    Inventory.PickupMessage "Grabbed a nifty, jumping sticky bomb!"
    Weapon.SelectionOrder 99
	Weapon.SlotNumber 0
	Weapon.SelectionOrder 10
    Weapon.AmmoType "StickyAmmo"
    Weapon.AmmoGive 5
    Weapon.AmmoUse 1
    Weapon.AmmoType2 "StickyThrowPower"
    Weapon.AmmoGive2 0
    Weapon.AmmoUse2 0
    Weapon.UpSound "StickyBomb/Up"
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobSpeed 1.6
	Weapon.BobRangeY 0.8
	Weapon.BobRangeX 1.0
    Scale 0.6
    +NOALERT
	+NOAUTOFIRE
    States
    {
    Spawn:
		SBMB A -1
		Stop
	Select:
		TNT1 A 0 A_TakeInventory("StickyThrowPower",0)
		TNT1 A 0 A_Raise(100)
		Wait
	Deselect:
		"####" "#" 0 A_JumpIfNoAmmo("InstantDeselect")
		"####" "#" 1 A_WeaponOffset(-11,7+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-25,17+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-36,29+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-47,50+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-52,70+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-55,94+32,WOF_INTERPOLATE)
	InstantDeselect:
		TNT1 A 0 A_WeaponOffset(0,32)
		TNT1 A 0 A_Lower(100)
		Wait
	Cancel:
		TNT1 A 0 {
			A_TakeInventory("StickyThrowPower",0);
			A_ClearRefire;
		}
	Ready:
		SBMG B 0 A_JumpIf(CountInv("StickyMode"),2)
		SBMG A 0
		"####" "#" 1 A_WeaponOffset(-55,94+32)
		"####" "#" 1 A_WeaponOffset(-52,70+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-47,50+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-36,29+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-25,17+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-11,7+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(0,32,WOF_INTERPOLATE)
	ReadyMain:
		SBMG B 0 A_JumpIf(CountInv("StickyMode"),2)
		SBMG A 0
		"####" "#" 1 A_WeaponReady
		Loop
	Fire:	
		"####" "#" 1 A_WeaponOffset(-3,2+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-16,6+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-28,10+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-46,15+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-73,29+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-88,47+32,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-100,75+32,WOF_INTERPOLATE)
	Hold:
		TNT1 A 0 A_GiveInventory("StickyThrowPower",1)
		TNT1 AAAAAAAA 1 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & BT_ALTATTACK, "Cancel")
		TNT1 A 0 A_ReFire
		SBMG B 0 A_JumpIf(CountInv("StickyMode"),2)
		SBMG A 0
		"####" "#" 1 A_WeaponOffset(-121,32+80)
		"####" "#" 1 A_WeaponOffset(-103,32+53,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-70,32+27,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-45,32+12,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(6,32-6,WOF_INTERPOLATE)
		SBMG C 0 {
			A_PlaySound("StickyBomb/Throw",1);
			If(CountInv("StickyMode")) {
				A_FireCustomMissile("ThrownSticky",frandom(1.0,-1.0),1,11,0,0,frandom(1.0,-1.0)); 
			} else {
				A_FireCustomMissile("ThrownBomb",frandom(1.0,-1.0),1,11,0,0,frandom(1.0,-1.0)); 
			}
		}
		"####" "#" 1 A_WeaponOffset(11,32-22,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(43,32-12,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(83,32+6,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(106,32+21,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(135,32+36,WOF_INTERPOLATE)
		TNT1 A 0 A_CheckReload
		TNT1 AAAAAA 1 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & BT_ATTACK, "TossAnother")
		Goto Ready
	TossAnother:
		TNT1 A 10
		Goto Hold
    AltFire:
		TNT1 A 0 A_JumpIf(CountInv("StickyMode"),"StickyIn")	
    StickyOut:
		SBMG A 8 {
			A_GiveInventory("StickyMode",1);
			A_PlaySound("StickyBomb/Mode",1);
		}
		SBMG B 8
		goto ReadyMain
    StickyIn:
		SBMG B 8 {
			A_TakeInventory("StickyMode",1); 
			A_PlaySound("StickyBomb/Mode",1);
		}
		SBMG A 8
		Goto ReadyMain
	}
}

actor ThrownBomb
{
    Radius 9
    Height 7
    Speed 20
    Damage 20
	BounceSound "StickyBomb/Bounce"
    BounceType "Hexen"
    BounceFactor 0.6
    BounceCount 5
    Gravity 0.65
    Scale 0.4
	Projectile
    -NOGRAVITY
    +CANBOUNCEWATER
    States
    {
    Spawn:
		TNT1 A 0 NoDelay {
			A_ScaleVelocity(1.0 + CountInv("StickyThrowPower",AAPTR_TARGET)*0.2);
			A_TakeInventory("StickyThrowPower",0,0,AAPTR_TARGET);
			A_ChangeVelocity(0,0,4 + CountInv("StickyThrowPower",AAPTR_TARGET)*0.3);
		}
		SBMB A 1
		Wait
    Death:
		SBMB A 15
    XDeath:
		TNT1 A 0 {
			A_AlertMonsters;
			A_PlaySound("BeefSupreme/Explode",5,1.0);
			A_SpawnItemEx("DeathHeadShock",0,0,20,0,0,0,0,SXF_CLIENTSIDE,0);
			A_SpawnItemEx("SteamExploder",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0);
			If(CountInv("NukerUpgrade",AAPTR_TARGET)) {
				A_Explode(800,384);
				A_SpawnItemEx("SteamShockNukerFlashSpawner",0,0,20,0,0,0,0,SXF_CLIENTSIDE,0);
				A_SpawnItemEx("SteamShockNukerBoomSpawner",0,0,20,0,0,0,0,SXF_TRANSFERPOINTERS,0);
				If(!GetCVar("RO_SmokeChoker")) {
					A_SpawnItemEx("SteamShockNukerSmokeSpawner",0,0,20,0,0,0,0,SXF_CLIENTSIDE,0);
				}
			} else {
				A_Explode(400,256);
				A_SpawnItemEx("SteamShockBoomSpawner",0,0,20,0,0,0,0,SXF_TRANSFERPOINTERS,0);
				If(!GetCVar("RO_SmokeChoker")) {
					A_SpawnItemEx("SteamShockSmokeSpawner",0,0,20,0,0,0,0,SXF_CLIENTSIDE,0);
				}
			}
		}
		TNT1 BCD 6
		Stop
    }
}

actor ThrownSticky : ThrownBomb
{
    Damage 0
    Health 40
    BounceType "None"
    +SHOOTABLE
    +SEEKERMISSILE
    +DONTSPLASH
    +DONTFALL
    -NOGRAVITY
    States
    {
    Spawn:
		TNT1 A 0 NoDelay {
			A_ScaleVelocity(1.0 + CountInv("StickyThrowPower",AAPTR_TARGET)*0.2);
			A_TakeInventory("StickyThrowPower",0,0,AAPTR_TARGET);
			A_ChangeVelocity(0,0,4 + CountInv("StickyThrowPower",AAPTR_TARGET)*0.3);
		}
		SBMB BC 3
		Goto Spawn+1
		SBMB BB 1 Bright {
			A_SeekerMissile(0,0,1,256,0);
			A_Fire(35);
		}
		SBMB BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 1 A_Fire(35)
		Goto StickyBombOnEnemy
    Death:
		TNT1 A 1 A_PlaySound("StickyBomb/Thud",4)
		TNT1 A 0 A_SpawnItemEx("StickyBombWallie",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
		Stop
    XDeath:
		TNT1 A 0 A_Stop
		Goto Spawn+4
    StickyBombOnEnemy:
		TNT1 A 0 A_PlaySound("StickyBomb/Activate",4,0.8)
		SBMB EEEEEEEEEE 1 Bright A_Fire(35)
		Goto Super::XDeath
    }
}

actor StickyBombWallie
{
	Radius 9
	Height 7
	Speed 0
	Health 10
	Mass 999999999
	+NOGRAVITY
	+FRIENDLY
	+SHOOTABLE
	+NOBLOOD
	+DONTFALL
	+NOICEDEATH
	+DONTRIP
	+LOOKALLAROUND
	Obituary "%o was facehugged by %k's jumping sticky bomb."
	Scale 0.4
	States
	{
	Spawn:
		SBMB BBBB 8 A_LookEx(LOF_NOSOUNDCHECK,0,256,0,0,"Whoopee")
		SBMB D 8 { A_LookEx(LOF_NOSOUNDCHECK,0,256,0,0,"Whoopee"); A_PlaySound ("StickyBomb/Tick",4,0.8); }
		Loop
	See:
		SBMB BBBB 8 A_Chase
		SBMB E 8 A_PlaySound("StickyBomb/Tick",4,0.8)
		Loop
	Whoopee:
		SBMB E 18 A_PlaySound("StickyBomb/Jump",4,0.8)
		SBMB B 0 A_CustomMissile("StickyJumper",0)
		Stop
	Death:
		TNT1 A 0 {
			A_AlertMonsters;
			A_PlaySound("BeefSupreme/Explode",5,1.0);
			A_SpawnItemEx("DeathHeadShock",0,0,20,0,0,0,0,SXF_CLIENTSIDE,0);
			A_SpawnItemEx("SteamExploder",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0);
			If(CountInv("NukerUpgrade",AAPTR_TARGET)) {
				A_Explode(800,384);
				A_SpawnItemEx("SteamShockNukerFlashSpawner",0,0,20,0,0,0,0,SXF_CLIENTSIDE,0);
				A_SpawnItemEx("SteamShockNukerBoomSpawner",0,0,20,0,0,0,0,SXF_TRANSFERPOINTERS,0);
				If(!GetCVar("RO_SmokeChoker")) {
					A_SpawnItemEx("SteamShockNukerSmokeSpawner",0,0,20,0,0,0,0,SXF_CLIENTSIDE,0);
				}
			} else {
				A_Explode(400,256);
				A_SpawnItemEx("SteamShockBoomSpawner",0,0,20,0,0,0,0,SXF_TRANSFERPOINTERS,0);
				If(!GetCVar("RO_SmokeChoker")) {
					A_SpawnItemEx("SteamShockSmokeSpawner",0,0,20,0,0,0,0,SXF_CLIENTSIDE,0);
				}
			}
		}
		TNT1 BCD 6
		Stop
  }
}

actor StickyJumper : ThrownBomb
{
    BounceType "None"
    States
    {
    Spawn:
		TNT1 A 0 NoDelay A_ChangeVelocity(0,0,10)
		SBMB BC 3
		Goto Spawn+1
    Death:
		TNT1 A 0 {
			A_PlaySound("StickyBomb/Thud");
			A_SpawnItemEx("StickyBombWallie",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS);
		}
		Stop
    }
}

actor LarpaTrail
{
	Scale 0.1
	Alpha 0.6
	Renderstyle "Add"
	+NOINTERACTION
	+THRUGHOST
	+DONTSPLASH
	+DONTBLAST
	States
	{
	Spawn:
		SMKE A 0 NoDelay A_Jump(256,2,4,6,8,10)
		SMKE ABCDEFGHJIKLMNOPQ 1 A_FadeOut(0.025)
		Stop
	}
}

actor LarpaPellet
{
	Radius 4
	Height 6
	Damage 25
	Projectile
	+RANDOMIZE
	-NOGRAVITY
	+THRUGHOST
	Gravity 0.35
	SeeSound ""
	DeathSound "Weapons/Larpaexp"
	Scale 0.45
	States
	{
	Spawn:
		LARP B 2 NoDelay A_SpawnItemEx("LarpaTrail",frandom(-1.5,1.5),frandom(-1.5,1.5),frandom(-1.5,1.5),0,0,0,0,SXF_CLIENTSIDE,0)
		loop
	Death:
		TNT1 A 0 {
			A_Explode(40,96,0);
			A_Explode(10,96);
			A_SpawnItemEx("PelletExplode",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0);
		}
		TNT1 BCD 4
		stop
	}
}

actor NukerLarpaPellet : LarpaPellet
{
	States
	{
	Death:
		TNT1 A 0 {
			A_Explode(80,144,0);
			A_Explode(20,144);
			A_SpawnItemEx("NukerPelletExplode",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0);
		}
		TNT1 BCD 4
		stop
	}
}

ACTOR LarpaPelletTrail : LarpaTrail
{
	Scale 0.05
	Alpha 0.35
	Renderstyle "Add"
}

//Quick throw
/*
	Select:
		TNT1 A 0 A_GiveInventory("StickyThrowPower",4)
		TNT1 A 1 A_Raise(100)
		Wait
	Ready:
		SBMG B 0 A_JumpIf(CountInv("StickyMode"),2)
		SBMG A 0
		"####" "#" 1 A_WeaponOffset(-121,32+80)
		"####" "#" 1 A_WeaponOffset(-103,32+53,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-70,32+27,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(-45,32+12,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(6,32-6,WOF_INTERPOLATE)
		SBMG C 0 {
			A_PlaySound("StickyBomb/Throw",1);
			If(CountInv("StickyMode")) {
				A_FireCustomMissile("ThrownSticky",frandom(1.0,-1.0),1,11,0,0,frandom(1.0,-1.0)); 
			} else {
				A_FireCustomMissile("ThrownBomb",frandom(1.0,-1.0),1,11,0,0,frandom(1.0,-1.0)); 
			}
		}
		"####" "#" 1 A_WeaponOffset(11,32-22,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(43,32-12,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(83,32+6,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(106,32+21,WOF_INTERPOLATE)
		"####" "#" 1 A_WeaponOffset(135,32+36,WOF_INTERPOLATE)
		Goto Deselect
	Deselect:
		TNT1 A 1 A_Lower(100)
		Loop*/