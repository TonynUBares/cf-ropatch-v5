/*
CREDITS:
Sprite - Marty Kirra
Pickup - Apogee (ROTT)

Sounds:
Up, Fire - ID (RTCW)
Add - ?
Die Waffen Legt An - Some military drill in germanic language Klepacki sampled to Hell March
March - Freesound\WebbFilmsUK
*/

actor MP40Side : Inventory { Inventory.MaxAmount 1 }
actor HellMarchCooldown : Inventory { Inventory.MaxAmount 700 }

Actor ROMP40Giver : CustomInventory
{
  Scale 0.8
  Inventory.Amount 1
  Inventory.MaxAmount 0
  Inventory.InterHubAmount 0
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You've got the MP40! HALT! *PEWPEWPEW-PEWPEW* (2)"
	Obituary "%o was grammar-nazi'd by %k's MP40."
  Tag "Ullapool Caber"
  States
  {
  Spawn:
	MPRG X 1
	Loop
  Pickup:
	TNT1 A 0 A_JumpIfInventory("MP40",1,"Remove")
  All:
	TNT1 A 0 A_GiveInventory("MP40",1)
	Stop
  Remove:
    TNT1 A 0
    Stop
  }
}


actor MP40 : DoomWeapon 24543 
{
	//$Category Weapons
	//$Title MP40
	//$Sprite MP40X0
	Inventory.MaxAmount 2
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You've got the MP40! HALT! *PEWPEWPEW-PEWPEW* (2)"
	Obituary "%o was grammar-nazi'd by %k's MP40."
	Weapon.UpSound "Weapons/MP40Up"
	Weapon.SelectionOrder 5
	Weapon.AmmoType "Boolet"
	Weapon.AmmoGive 30
	Weapon.AmmoUse 1
	Weapon.AmmoType2 "Boolet"
	Weapon.AmmoUse2 1
	Weapon.SlotNumber 2
	Weapon.SlotPriority 29.0
	Weapon.BobStyle "InverseSmooth"
	Weapon.BobSpeed 1.5
	Weapon.BobRangeY 0.6
	Weapon.BobRangeX 1.2
	AttackSound "Weapons/MP40Fire"
	Scale 0.5
	+WEAPON.CHEATNOTWEAPON
	+NOALERT
	States
	{
	DeadLowered:
		TNT1 A -1
		Stop
	Ready:
		MPRG A 0 {
			A_Overlay(2,"LeftReady");
			A_Overlay(3,"RightReady");
		}
	ReadyMain:
		TNT1 A 0 A_JumpIf(CountInv("MP40") >= 2,2)
		MP4G A 1 A_WeaponReady
		Loop
		TNT1 A 1 A_WeaponReady(WRF_NOPRIMARY)
		Loop
	LeftReady:
		MPRG A 0 {
			A_OverlayFlags(OverlayID(),PSPF_MIRROR|PSPF_FLIP,1);
			A_OverlayFlags(OverlayID(),PSPF_ADDWEAPON,0);
			A_OverlayOffset(OverlayID(),0,32);
		}
		TNT1 A 1 A_JumpIf(CountInv("MP40") >= 2,1)
		Loop
		TNT1 A 0 { If(CountInv("MP40Side") && GetPlayerInput(INPUT_BUTTONS) & BT_ATTACK) { Return State("LeftFire"); } Return State(""); }
		MPRG A 1
		Loop
	RightReady:
		MPRG A 0 {
			A_OverlayFlags(OverlayID(),PSPF_ADDWEAPON,0);
			A_OverlayOffset(OverlayID(),0,32);
		}
		TNT1 A 1 A_JumpIf(CountInv("MP40") >= 2,1)
		Loop
		TNT1 A 0 { If(!CountInv("MP40Side") && GetPlayerInput(INPUT_BUTTONS) & BT_ATTACK) { Return State("RightFire"); } Return State(""); }
		MPRG A 1
		Loop
	Deselect:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,1,1,0)
	DeselectLoop:
		MPRG A 0 A_Overlay(3,"None")
		MP4G A 0 A_JumpIf(CountInv("MP40") < 2,2) 
		MPRG A 0 A_Overlay(2,"DualDeselect")
		"####" A 1 A_Lower(12)
		Wait
	DualSelect:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,0,1,0)
	DualDeselect:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,1,1,0)
	DualDeselectLoop:
		MPRG A 0 A_OverlayFlags(OverlayID(),PSPF_MIRROR|PSPF_FLIP,1)
		MPRG A 1
		Wait
	Select:
		TNT1 A 0 ACS_NamedExecuteAlways("SbarinfoFix",0,0,1,0)
	SelectLoop:
		MPRG A 0 A_Overlay(3,"None")
		MP4G A 0 A_JumpIf(CountInv("MP40") < 2,2) 
		MPRG A 0 A_Overlay(2,"DualSelect")
		"####" A 1 A_Raise(12)
		Wait
	Fire:
		MP4F A 0 A_JumpIf(CountInv("MP40") < 2,2) 
		MPRF A 0 
		"####" A 0 A_CheckReload
		"####" A 0 { If(CountInv("MP40Side")) { Return State("ReadyMain"); } Return State(""); }
		"####" A 1 Bright {
			A_AlertMonsters;
			A_OverlayOffset(OverlayID(),3,35,WOF_INTERPOLATE);
			A_GiveInventory("BooletCasing");
			A_PlaySound("Weapons/MP40AddFire",5,0.2);
			if(CountInv("CaliberUpgrade")) {
				A_PlaySound("Weapons/WolgirHEndFire",6,0.8);
				A_FireBullets(3.0, 1.0, -1, 80, "CaliberPuff");
			} else {
				A_FireBullets(3.0, 1.0, -1, 40, "BulletPuff");
			}
			//A_SetAngle(Angle-0.1,SPF_INTERPOLATE);
			//A_SetPitch(Pitch-0.3,SPF_INTERPOLATE);
		}
		"####" B 1 Bright A_OverlayOffset(OverlayID(),6,38,WOF_INTERPOLATE)
		"####" C 1 { A_OverlayOffset(OverlayID(),7,39,WOF_INTERPOLATE); if(CountInv("MP40") >= 2) { A_GiveInventory("MP40Side",1); } }
		"####" D 1 A_OverlayOffset(OverlayID(),4,36,WOF_INTERPOLATE)
		MP4G A 0 A_JumpIf(CountInv("MP40") < 2,2) 
		MPRG A 0 
		"####" A 0 A_Jump(208,"ReadyMain")
		"####" A 1 A_OverlayOffset(OverlayID(),2,34,WOF_INTERPOLATE)
		//"####" A 0 A_OverlayOffset(1,0,32,WOF_INTERPOLATE)
		Goto ReadyMain
	LeftFire:
		MPRG A 0 A_CheckReload
		MPRF A 1 Bright {
			A_AlertMonsters;
			A_OverlayOffset(OverlayID(),3,35,WOF_INTERPOLATE);
			A_GiveInventory("BooletCasing");
			A_PlaySound("Weapons/MP40AddFire",5,0.2);
			if(CountInv("CaliberUpgrade")) {
				A_PlaySound("Weapons/WolgirHEndFire",6,0.8);
				A_FireBullets(3.0, 1.0, -1, 80, "CaliberPuff");
			} else {
				A_FireBullets(3.0, 1.0, -1, 40, "BulletPuff");
			}
			//A_SetAngle(Angle+0.1,SPF_INTERPOLATE);
			//A_SetPitch(Pitch-0.3,SPF_INTERPOLATE);
		}
		MPRF B 1 Bright A_OverlayOffset(OverlayID(),6,38,WOF_INTERPOLATE)
		MPRF C 1 { A_OverlayOffset(OverlayID(),7,39,WOF_INTERPOLATE); if(CountInv("MP40") >= 2) { A_TakeInventory("MP40Side",1); } }
		MPRF D 1 A_OverlayOffset(OverlayID(),4,36,WOF_INTERPOLATE)
		MPRG A 0 A_Jump(208,"LeftReady")
		MPRG A 1 A_OverlayOffset(OverlayID(),2,34,WOF_INTERPOLATE)
		//MPRG A 0 A_OverlayOffset(OverlayID(),0,32,WOF_INTERPOLATE)
		Goto LeftReady
	RightFire:
		MPRG A 0 A_CheckReload
		"####" A 0 { If(CountInv("MP40Side")) { Return State("RightReady"); } Return State(""); }
		MPRF A 1 Bright {
			A_AlertMonsters;
			A_OverlayOffset(OverlayID(),3,35,WOF_INTERPOLATE);
			A_GiveInventory("BooletCasing");
			A_PlaySound("Weapons/MP40AddFire",5,0.2);
			if(CountInv("CaliberUpgrade")) {
				A_PlaySound("Weapons/WolgirHEndFire",6,0.8);
				A_FireBullets(3.0, 1.0, -1, 80, "CaliberPuff");
			} else {
				A_FireBullets(3.0, 1.0, -1, 40, "BulletPuff");
			}
			//A_SetAngle(Angle+0.1,SPF_INTERPOLATE);
			//A_SetPitch(Pitch-0.3,SPF_INTERPOLATE);
		}
		MPRF B 1 Bright A_OverlayOffset(OverlayID(),6,38,WOF_INTERPOLATE)
		MPRF C 1 { A_OverlayOffset(OverlayID(),7,39,WOF_INTERPOLATE); if(CountInv("MP40") >= 2) { A_GiveInventory("MP40Side",1); } }
		MPRF D 1 A_OverlayOffset(OverlayID(),4,36,WOF_INTERPOLATE)
		MPRG A 0 A_Jump(208,"RightReady")
		MPRG A 1 A_OverlayOffset(OverlayID(),2,34,WOF_INTERPOLATE)
		//MPRG A 0 A_OverlayOffset(OverlayID(),0,32,WOF_INTERPOLATE)
		Goto RightReady
	AltFire:
		MPRG A 0 {
			If(!CountInv("HellMarchCooldown")) {
				A_GiveInventory("HellMarchCooldown",700);
				A_SpawnItemEx("HellMarchProjectile",0,0,height*0.5,cos(pitch)*300,0,-sin(pitch)*300,0,SXF_SETTARGET|SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH);
				Return State("Delay");
			}
			Return State("");
		}
		TNT1 A 0 A_JumpIf(CountInv("MP40") >= 2,2)
		MP4G A 0
		"####" "#" 1
		Goto ReadyMain
	Delay:
		TNT1 A 0 A_JumpIf(CountInv("MP40") >= 2,2)
		MP4G A 0
		"####" "#" 35
		Goto ReadyMain
	Spawn:
		MP40 X -1
		Stop
	}
}

actor HellMarchProjectile : FastProjectile
{
	Radius 3
	Height 3
	Speed 300
	Projectile
	States
	{
	Spawn:
		TNT1 A 1
		Loop
	Death:
		TNT1 A 0 {
			A_SpawnItemEx("MarchingNaziColumn",512,-384,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION);
			A_AlertMonsters;
		}
		TNT1 A 133 A_PlaySound("DieWaffen/LegtAn",5)
		TNT1 A 0 A_SpawnItemEx("MarchingNaziSounder",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		Stop
	}
}

actor MarchingNaziSounder
{
	+NOCLIP
	var int user_timer;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			user_timer = 21;
		}
		TNT1 A 0 A_ChangeVelocity(5,0,0,CVF_RELATIVE)
		TNT1 A 12
		TNT1 A random(5,6) { A_Stop; A_AlertMonsters; /*A_PlaySound("DieWaffen/March1",5);*/ }
		TNT1 A 0 A_ChangeVelocity(5,0,0,CVF_RELATIVE)
		TNT1 A 12
		TNT1 A random(5,6) { A_Stop; A_AlertMonsters; /*A_PlaySound("DieWaffen/March2",5);*/ }
		TNT1 D 0 {
			user_timer--;
			Return A_JumpIf(user_timer <= 0, "Dissipate");
		}
		Goto Spawn+1
	Dissipate:
		TNT1 A 0 A_ChangeVelocity(5,0,0,CVF_RELATIVE)
		TNT1 A 12
		TNT1 A random(5,6) { A_Stop; A_AlertMonsters; /*A_PlaySound("DieWaffen/March1",5);*/ }
		TNT1 A 0 A_ChangeVelocity(5,0,0,CVF_RELATIVE)
		TNT1 A 12
		TNT1 A random(5,6) { A_Stop; A_AlertMonsters; /*A_PlaySound("DieWaffen/March2",5);*/ }
		Stop
	}
}

actor MarchingNaziColumn
{
	+NOINTERACTION
	var int user_range;
	var int user_row;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			user_row = 24;
			user_range = 0;
		}
		TNT1 A 0 {
			A_SpawnItemEx("MarchingNaziRow",0,user_range,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION);
			user_range = user_range + 32;
			user_row--;
			Return A_JumpIf(user_row <= 0, "Null");
		}
		Wait
	}
}

actor MarchingNaziRow : MarchingNaziColumn
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			user_row = 32;
			user_range = 0;
		}
		TNT1 A 0 {
			A_SpawnItemEx("MarchingNazi",user_range,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION);
			user_range = user_range - 32;
			user_row--;
			Return A_JumpIf(user_row <= 0, "Null");
		}
		Wait
	}
}

actor MarchingNazi
{
	+NOCLIP
	+NODAMAGETHRUST
	+FORCERADIUSDMG
	ProjectileKickback 0
	Obituary "%o felt the might of the parade march."
	YScale 1.2
	Alpha 0.0
	var int user_timer;
	States
	{
	Spawn:
		SSHM AAAAAAAAAA 1 NoDelay A_FadeIn(0.1)
		SSHM A 123 {
			//A_PlaySound("DieWaffen/March",5);
			user_timer = 21;
		}
	March:
		SSHM A 0 A_ChangeVelocity(8,0,0,CVF_RELATIVE)
		SSHM AB 6
		SSHM B random(5,6) {
			A_PlaySound("DieWaffen/March1",5);
			A_Explode(35,64,0);
			A_Stop;
		}
		SSHM A 0 A_ChangeVelocity(8,0,0,CVF_RELATIVE)
		SSHM CD 6
		SSHM D random(5,6) {
			A_PlaySound("DieWaffen/March2",5);
			A_Explode(35,64,0);
			A_Stop;
		}
		SSHM D 0 {
			user_timer--;
			Return A_JumpIf(user_timer <= 0, "Dissipate");
		}
		Loop
	Dissipate:
		SSHM A 0 A_ChangeVelocity(8,0,0,CVF_RELATIVE)
		SSHM AB 6 A_FadeOut(0.2)
		SSHM B random(5,6) {
			A_PlaySound("DieWaffen/March1",5);
			A_FadeOut(0.2);
			A_Explode(40,64,0);
			A_Stop;
		}
		SSHM A 0 A_ChangeVelocity(8,0,0,CVF_RELATIVE)
		SSHM CD 6 A_FadeOut(0.2)
		SSHM D random(5,6) {
			A_PlaySound("DieWaffen/March2",5);
			A_Explode(40,64,0);
			A_Stop;
		}
		Stop
	}
}
